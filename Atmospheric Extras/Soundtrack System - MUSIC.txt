@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Soundtrack Management System <MUSIC>,music,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+SOUNDTRACK`PENNMUSH [u(cobj,music)]=$^(?s)(?\:\+)?(?\:soundtrack|music)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+SOUNDTRACK`MAIN
@set [u(cobj,music)]/CMD`+SOUNDTRACK`PENNMUSH=regexp
&CMD`+SOUNDTRACK`RHOSTMUSH [u(cobj,music)]=$^(?s)(?\:\+)?(?\:soundtrack|music)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+SOUNDTRACK`MAIN
@set [u(cobj,music)]/CMD`+SOUNDTRACK`RHOSTMUSH=regexp
&CMD`+SOUNDTRACK`MAIN [u(cobj,music)]=th u(setq,sysname,%1);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2,%3,%4
@set [u(cobj,music)]/CMD`+SOUNDTRACK`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`ADMIN [u(cobj,music)]=SET|DEL
&SYSTEm`NAME [u(cobj,music)]=SOUNDTRACK

&INC`MAIN [u(cobj,music)]=@check u(setr,total,nattr(%l/MUSIC`*))=@attach %!/INC`MSG=Sorry, nothing here to hear!;@pemit %#=u(header,Soundtrack for [u(strfirstof,u(%l/COLORNAME),name(%l))]);@dolist/inline u(sortattr,u(lattr,%l/MUSIC`*))={@pemit %#=align(4 19 55,rjust(last(%i0,`),3,0).,ansi(h,default(%l/%i0,Unnamed)),u(weblink,get(%l/%i0`URL),get(%l/%i0`URL)))};@pemit %#=u(subheader,Total Songs: %q<total>)

&INC`SET [u(cobj,music)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No track number entered.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: Track Numbers must be whole\, positive numbers.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No track name entered.;@check strlen(%2)=@attach %!/INC`MSG=ERROR: URL for the song empty!;&MUSIC`%0 %l=%1;&MUSIC`%0`URL %l=%2;@attach %!/INC`MSG=Soundtrack Entry Set: Track %0: - %1 - [u(weblink,%2,%2)]

&INC`DEL [u(cobj,music)]=@check u(setr,total,nattr(%l/MUSIC`*))=@attach %!/INC`MSG=Sorry\, nothing here to hear!;@check strlen(%0)=@attach %!/INC`MSG=ERROR: No track number entered.;@check u(valnum,%0)=@attach %!/INC`MSG=ERROR: Track Numbers must be whole\, positive numbers.;@check hasattr(%l/MUSIC`%0)=@attach %!/INC`MSG=ERROR: No Track here by that number.;@attach %!/WIPE=%l,MUSIC`%0;@attach %!/INC`MSG=Track deleted!
