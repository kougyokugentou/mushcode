@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Report Management System <REPORT>,report,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,Report Database <REPDB>,repdb,u(cobj,report),,1,WIZARD SAFE,INHERIT SIDEFX SAFE)

&CMD`+REPORT`PENNMUSH [u(cobj,report)]=$^(?s)(?\:\+)?report(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+REPORT`MAIN
@set [u(cobj,report)]/CMD`+REPORT`PENNMUSH=regexp
&CMD`+REPORT`RHOSTMUSH [u(cobj,report)]=$^(?s)(?\:\+)?report(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+REPORT`MAIN
@set [u(cobj,report)]/CMD`+REPORT`RHOSTMUSH=regexp
&CMD`+REPORT`MAIN [u(cobj,report)]=th u(setq,sysname,%1);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=trim(%2),trim(%3),trim(before(%2,/)),trim(after(%2,/)),trim(before(%3,/)),trim(after(%3,/))
@set [u(cobj,report)]/CMD`+REPORT`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,report)]=
&SWITCHES`ADMIN [u(cobj,report)]=ADDSECTION|DELSECTION|RENSECTION|ORDSECTION|ADDPARAMETER|DELPARAMETER|RENPARAMETER|ORDPARAMETER|SET
&SYSTEM`NAME [u(cobj,report)]=REPORT

&INC`ADDSECTION [u(cobj,report)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No Section name entered.;@stop words(u(wildgrepi,u(cobj,repdb),*,%0))=@attach %!/INC`MSG=ERROR: That name is already in use.;th u(attrib_set,u(cobj,repdb),SEC`[u(nextslot,u(cobj,repdb),SEC)],%0);@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] New Report Section added: %0};@attach %!/INC`MSG=Added new Report Section: %0

&INC`DELSECTION [u(cobj,report)]=@attach %!/INC`FINDSECTION=%0;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will delete Report Section %q<section>: [get(u(cobj,repdb)/%q<section>)]. All Parameters and change history will be lost. Are you sure? Enter the same command again within 10 seconds to verify.},DELETE REPORT SECTION %q<section>;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section DELETED: %q<section> - [get(u(cobj,repdb)/%q<section>)]};@attach %!/INC`MSG=Deleted!;@attach %!/WIPE=u(cobj,repdb),%q<section>

&INC`RENSECTION [u(cobj,report)]=@attach %!/INC`FINDSECTION=%0;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No new Section name entered.;@stop words(u(wildgrepi,u(cobj,repdb),*,%1))=@attach %!/INC`MSG=ERROR: That name is already in use.;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section RENAMED: %q<section> - [get(u(cobj,repdb)/%q<section>)] changed to %1};@attach %!/INC`MSG=Renamed!;&%q<section> u(cobj,repdb)=%1

&INC`ORDSECTION [u(cobj,report)]=@attach %!/INC`FINDSECTION=%0;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No Section order value entered.;@check u(valnum,%1)=@attach %!/INC`MSG=Section order must be a positive integer.;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section RE-ORDERED: %q<section> - [get(u(cobj,repdb)/%q<section>)] ORDER is now: %1};@attach %!/INC`MSG=Re-ordered!;&%q<section>`ORDER u(cobj,repdb)=%1

&INC`FINDSECTION [u(cobj,report)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No Section targeted.;@check strlen(u(setr,section,u(FUN`FINDSECTION,%0)))=@attach %!/INC`MSG=ERROR: Section '%0' not found.;th u(setq,secname,get(u(cobj,repdb)/%q<section>))

&FUN`FINDSECTION [u(cobj,report)]=u(strfirstof,first(u(wildgrepi,u(cobj,repdb),SEC`*,%0)),first(u(wildgrepi,u(cobj,repdb),SEC`*,%0*)))

&INC`ADDPARAMETER [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@check strlen(%3)=@attach %!/INC`MSG=ERROR: No parameter name entered.;@stop words(u(wildgrepi,u(cobj,repdb),%q<section>`PARA`*,%0))=@attach %!/INC`MSG=ERROR: That name is already in use.;@check strlen(%4)=@attach %!/INC`MSG=ERROR: No initial value entered!;@check strlen(%5)=@attach %!/INC`MSG=ERROR: no initial reason entered!;th u(attrib_set,u(cobj,repdb),u(setr,attr,%q<section>`PARA`[u(setr,parameter,u(nextslot,u(cobj,repdb),%q<section>`PARA))]),%3);@attach %!/INC`NEWENTRY=%q<section>,%q<parameter>,%4,%5,u(objid,%#);@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section '%q<secname>' has New Parameter '%3' With initial value: %4 BECAUSE %5};@attach %!/INC`MSG=Added new Parameter: %3

&INC`FINDPARAMETER [u(cobj,report)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No Parameter targeted.;@check strlen(u(setr,parameter,u(FUN`FINDPARAMETER,%q<section>,%0)))=@attach %!/INC`MSG=ERROR: Parameter '%0' not found.;th u(setq,paraname,get(u(cobj,repdb)/%q<section>`PARA`%q<parameter>))

&FUN`FINDPARAMETER [u(cobj,report)]=last(u(strfirstof,first(u(wildgrepi,u(cobj,repdb),%0`PARA`*,%1)),first(u(wildgrepi,u(cobj,repdb),%0`PARA`*,%1*))),`)

&INC`NEWENTRY [u(cobj,report)]=th u(attrib_set,u(cobj,repdb),u(setr,entry,%0`PARA`%1`ENTRY`[u(nextslot,u(cobj,repdb),%0`PARA`%1`ENTRY)]),%2);&%q<entry>`REASON u(cobj,repdb)=%3;&%q<entry>`ON u(cobj,repdb)=secs();&%q<entry>`BY u(cobj,repdb)=%4;&%q<entry>`BYNAME u(cobj,repdb)=name(%4);&%0`PARA`%1`VALUE u(cobj,repdb)=%2;&%0`PARA`%1`REASON u(cobj,repdb)=%3;&%0`PARA`%1`ON u(cobj,repdb)=secs();&%0`PARA`%1`BY u(cobj,repdb)=%4;&%0`PARA`%1`BYNAME u(cobj,repdb)=name(%4)

&INC`DELPARAMETER [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@attach %!/INC`FINDPARAMETER=%3;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will delete Section '%q<secname>'s Parameter '%q<paraname>'. All change history will be lost. Are you sure? Enter the same command again within 10 seconds to verify.},DELETE REPORT SECTION %q<section> PARAMETER %q<parameter>;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section '%q<secname>' Parameter '%q<paraname>' DELETED};@attach %!/INC`MSG=Deleted!;@attach %!/WIPE=u(cobj,repdb),%q<section>`PARA`%q<parameter>

&INC`RENPARAMETER [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@attach %!/INC`FINDPARAMETER=%3;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No new Parameter name entered.;@stop words(u(wildgrepi,u(cobj,repdb),%q<section>`PARA`*,%1))=@attach %!/INC`MSG=ERROR: That name is already in use.;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section '%q<secname>' Parameter '%q<paraname>' RENAMED to: %1};@attach %!/INC`MSG=Renamed!;&%q<section>`PARA`%q<parameter> u(cobj,repdb)=%1

&INC`ORDPARAMETER [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@attach %!/INC`FINDPARAMETER=%3;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No new Parameter order value entered.;@check u(valnum,%1)=@attach %!/INC`MSG=ERROR: Order values must be positive integers.;@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section '%q<secname>' Parameter '%q<paraname>' ORDER set to: %1};@attach %!/INC`MSG=Re-ordered!;&%q<section>`PARA`%q<parameter>`ORDER u(cobj,repdb)=%1

&INC`SET [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@attach %!/INC`FINDPARAMETER=%3;@check strlen(%4)=@attach %!/INC`MSG=ERROR: No value entered!;@check strlen(%5)=@attach %!/INC`MSG=ERROR: No reason entered!;@attach %!/INC`NEWENTRY=%q<section>,%q<parameter>,%4,%5,u(objid,%#);@attach %!/INC`MSG`CHAN={[ansi(h,\[%n\])] Report Section '%q<secname>' Parameter '%q<paraname>' set to '%4' BECAUSE '%5'};@attach %!/INC`MSG=Set!

&INC`MAIN [u(cobj,report)]=@select/inline t(strlen(%2))[t(strlen(%3))]=0*,{@attach %!/INC`DISPLAYALL},10,{@attach %!/INC`DISPLAYSECTION},11,{@attach %!/INC`DISPLAYPARAMETER}

&INC`DISPLAYALL [u(cobj,report)]=@pemit %#=u(HEADER,Situation Report);@dolist/inline sortkey(#lambda/u(strfirstof,get(u(cobj,repdb)/\%0`ORDER),99),u(sortattr,u(lattr,u(cobj,repdb)/SEC`*)))={@pemit %#=u(SEPARATOR,get(u(cobj,repdb)/%i0) Report);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),align(14 13 49,Type,Status,Reason));@pemit %#=u(separator);@dolist/inline sortkey(#lambda/u(strfirstof,get(u(cobj,repdb)/\%0`ORDER),99),sort(u(lattr,u(cobj,repdb)/%i0`PARA`*)))={@pemit %#=align(14 13 49,u(pueblize,get(u(cobj,repdb)/%i0),+report [get(u(cobj,repdb)/%i1)]/[get(u(cobj,repdb)/%i0)]),get(u(cobj,repdb)/%i0`VALUE),get(u(cobj,repdb)/%i0`REASON))}};@pemit %#=u(SUBHEADER)

&INC`DISPLAYSECTION [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@pemit %#=u(HEADER,get(u(cobj,repdb)/%q<section>) Report);@pemit %#=align(14 13 49,Type,Status,Reason);@dolist/inline sortkey(#lambda/u(strfirstof,get(u(cobj,repdb)/\%0`ORDER),99),sort(u(lattr,u(cobj,repdb)/%q<section>`PARA`*)))={@pemit %#=align(14 13 49,u(pueblize,get(u(cobj,repdb)/%i0),+report %q<secname>/[get(u(cobj,repdb)/%i0)]),get(u(cobj,repdb)/%i0`VALUE),get(u(cobj,repdb)/%i0`REASON))};@pemit %#=u(SUBHEADER)

&INC`DISPLAYPARAMETER [u(cobj,report)]=@attach %!/INC`FINDSECTION=%2;@attach %!/INC`FINDPARAMETER=%3;th u(setq,attr,%q<section>`PARA`%q<parameter>);@pemit %#=u(HEADER,%q<secname> Report: %q<paraname>);@pemit %#=ansi(u(color,%#,COLOR,COLUMN_NAMES),if(u(isadmin,%#),align(15 30 18 12,Status,Reason,Changed,By),align(15 30 18,Status,Reason,Changed)));@pemit %#=u(separator);@dolist/inline u(sortattr,u(lattr,u(cobj,repdb)/%q<attr>`ENTRY`*))={@pemit %#=if(u(isadmin,%#),align(15 30 18 12,get(u(cobj,repdb)/%i0),get(u(cobj,repdb)/%i0`REASON),u(fancytime,get(u(cobj,repdb)/%i0`ON),%#),name(get(u(cobj,repdb)/%i0`BY))),align(15 30 18,get(u(cobj,repdb)/%i0),get(u(cobj,repdb)/%i0`REASON),u(fancytime,get(u(cobj,repdb)/%i0`ON),%#)))};@pemit %#=u(SUBHEADER)

@@ CHARACTER - +REPORT
+help/add +report=[u(cobj,report)]/HLP`+REPORT
+help/category +report=Roleplaying
&HLP`+REPORT [u(cobj,report)]=The Report system keeps track of a number of IC statistics for the setting. %R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+report)] - Display a Summary of the IC statistics.%R[ansi(h,+report <section>)] - Display a summary for only one section.%R[ansi(h,+report <section>/<parameter>)] - Display the history of changes for a parameter.)]

+shelp/add +REPORT=[u(cobj,report)]/SHLP`+REPORT
+shelp/category +report=Roleplaying
&SHLP`+REPORT [u(cobj,report)]=Only Staff can change the statistics and layout of the +report display.%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+report/addsection <section>)] - Create a whole new section.%R[ansi(h,+report/delsection <section>)] - Remove a section and all its parameters.%R[ansi(h,+report/rensection <section>=<newname>)] - Rename a section.%R[ansi(h,+report/ordsection <section>=<order #>)] - Change the display order for a section. Lower numbers display higher up.%R%R[ansi(h,+report/addparameter <section>/<parameter>=<value>/<reason>)] - Create a new parameter named <parameter> with a starting <value> and <reason>.%R[ansi(h,+report/delparameter <section>/<parameter>)] - Delete a section and all its change history.%R[ansi(h,+report/renparameter <section>/<parameter>=<newname>)] - Rename a parameter.%R[ansi(h,+report/ordparameter <section>/<parameter>=<order #>)] - Change display order for a parameter within a section.%R%R[ansi(h,+report/set <section>/<parameter>=<value>/<reason>)] - Update a parameter with a new value.)]


&MIGRATE`REPORT [u(cobj,migrate)]=@check isdbref(u(setr,repdb,u(coi,repdb)));@dolist reglattr(%q<repdb>/^\\d+$)={@attach %!/INC`CPTREE=%q<repdb>,%i0,u(cobj,repdb),SEC`%i0}
