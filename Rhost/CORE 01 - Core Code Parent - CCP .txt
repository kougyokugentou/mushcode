@@ COMMENTS WILL GO HERE

@@ RUN THIS ON CODE WIZARD OBJECT!
@switch/first/inline version()=PennMUSH*,{@switch/first/inline hasflag(%#,UNREGISTERED)=1,{@set me=!UNREGISTERED};@power me=no_pay},RhostMUSH*,{@set me=sidefx !wanderer;@admin command_quota_max=10000;@admin command_quota_increment=100}

th if(isdbref(u(cobj,mco)),,setq(0,create(Master Code Object <MCO>))[tel(%q0,switch(version(),PennMUSH*,config(master_room),RhostMUSH**,globalroom()))][parent(%#,%q0)][set(%q0,VA:[first(version())])][set(%q0,COBJ`MCO:[switch(version(),PennMUSH*,objid(%q0),RhostMUSH*,%q0)])][set(%q0,switch(version(),PennMUSH*,WIZARD SAFE,RhostMUSH*,INHERIT SAFE SIDEFX))])
@@ Yeah, it's an awful hodgepodge of side effect functions. For a good cause.

&COBJ [parent(%#)]=v(COBJ`%0)
&VA [parent(%#)]=RhostMUSH
&GAME [parent(%#)]=RhostMUSH

&COI [u(cobj,mco)]=localize(if(isdbref(u(setr,db,u(find_in,u(master_room),Code Object Index <COI>))),u(%q<db>/%0)))

&IS_ANCESTOR [u(cobj,mco)]=cand(hasattr(u(cobj,mco),COBJ`ANCESTOR_PLAYER),strmatch(num(%0),num(u(cobj,ancestor_player))))

&ATTRIB_SET [u(cobj,mco)]=if(u(IS_ANCESTOR,%0),,set(%0,%1:%2,strmatch(%1,*`*)))

&LATTR [u(cobj,mco)]=if(strlen(%1),iter(lattr(%0,,,,,1),%i0,%b,%1),lattr(%0,,,,,1))

&LATTRP [u(cobj,mco)]=if(strlen(%1),iter(lattrp(%0,,,,,1),%i0,%b,%1),lattrp(%0,,,,,1))

&REGLATTR [u(cobj,mco)]=if(strlen(%1),iter(lattr(%0,,,,1,1),%i0,%b,%1),lattr(%0,,,,1,1))

&STRINGSECS [u(cobj,mco)]=ladd(iter(secure(regeditall(%0,(h|m|s),$1%b)),switch(%i0,*ML,mul(%i0,31536000000),*y,mul(%i0,31471200),*C,mul(%i0,3153600000),*Mo,mul(%i0,2628000),*w,mul(%i0,604800),*d,mul(%i0,86400),*h,mul(%i0,3600),*m,mul(%i0,60),*s,add(%i0,0))) 0)

&SETQ [u(cobj,mco)]=setq(+,%1,%0)

&SETR [u(cobj,mco)]=setr(+,%1,%0)

&STARTUP [u(cobj,mco)]=@dolist/inline u(lattr,%!/STARTUP`*)={@include/nobreak %!/##}

&STARTUP`FUNCTIONS [u(cobj,mco)]=@dolist/inline setunion(game_config header subheader footer separator isadmin isapproved approved sortname color mxpmenu pueblize my_config my_names speech markup colormarkup chancolor colornames choosegame sortorder namelink weblink getproperty hideidle lastidle,v(FUNCTIONLIST`EXTRA))={@function/privileged vol_%d0=%!/%d0};@dolist/inline v(FUNCTIONLIST`ADMIN)={@admin function_access=vol_%d0 wizard};@dolist/inline moniker lvplayers lvthings lvexits objid align lalign sortkey strfirstof table width=@function/privileged %d0=%!/%d0

@@ &FUNCTIONLIST`EXTRA [u(cobj,mco)]=
@@ &FUNCTIONLIST`ADMIN [u(cobj,mco)]=
@@ These are meant to be added to by game packages.

&STARTUP`MAIN [u(cobj,mco)]=@include/nobreak %!/STARTUP`MAIN`[%va];@include/nobreak %!/STARTUP`MAIN`EXTRA

&STARTUP`MAIN`PENNMUSH [u(cobj,mco)]=@dolist/inline @name @nuke @toad @turtle={@hook/before %d0;@hook/after %d0}

@DESCRIBE [u(cobj,mco)]=This is Code Wizard territory. No touching!

@@ FILTER SECTION

&FIL`ISDBREF [u(cobj,mco)]=isdbref(%0)
&FIL`ISOBJID [u(cobj,mco)]=isobjid(%0)
&FIL`ISADMIN [u(cobj,mco)]=u(isadmin,%0)
&FIL`OLDOBJID [u(cobj,mco)]=!isobjid(before(%0,~))
&FIL`ISSLOT [u(cobj,mco)]=isint(last(%0,`))
&FIL`ALIASMATCH [u(cobj,mco)]=strmatch(alias(%0),%1*)
&FIL`NOTGUEST [u(cobj,mco)]=!u(isguest,%0)
&FIL`FINDACCOUNT [u(cobj,mco)]=match(get(%0/CHARACTERS),%q<finddb>)
&FIL`STRLEN [u(cobj,mco)]=t(strlen(%0))
&FIL`ISINT [u(Cobj,mco)]=isint(%0)
&FIL`LISTENRHOST [u(cobj,mco)]=orflags(%0,pc)
&FIL`HASATTR [u(Cobj,mco)]=hasattr(%0/%1)

@@ LOCK MANAGER
&INC`GROUPLOCK [u(cobj,mco)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No lock entered!;@check u(lmin,iter(%0,cand(isdbref(u(FUN`FINDGROUP,before(%i0,/),%#)),cor(!strlen(after(%i0,/)),cand(isint(%i0,/),gte(after(%i0,/),0)))),|,%B))=@attach %!/INC`MSG=ERROR: One or more group entries did not validate. Group names must be typed correctly and the /rank must be an integer.;th u(setq,lockstr,u(GROUP_LOCK,%0))

&GROUP_LOCK [u(cobj,mco)]=localize(iter(%0,V`GROUP`[get(u(u(cobj,gms)/FUN`FINDGROUP,before(%i0,/),%1)/D`ID)]:<=[u(strfirstof,after(%i0,/),9001)],|,|))

@@ U-FUNCTION SECTION



&NEWCOBJ [u(cobj,mco)]=localize(if(!isobjid(u(cobj,%1)),u(setr,newobj,objid(create(%0)))[if(!%4,parent(%q<newobj>,u(firstof,%3,u(cobj,mco))))][tel(%q<newobj>,u(firstof,%2,u(MASTER_ROOM)))][u(attrib_set,u(cobj,mco),COBJ`%1,%q<newobj>)][set(%q<newobj>,u(choosegame,%5,%6))]))
@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

&CANHEAR [u(cobj,mco)]=u(filter,LISTENRHOST,%0)


&FIND_IN [u(cobj,mco)]=u(firstof,locate(%0,%1,Tia),locate(%0,%1,Ti))

&ACCID [u(cobj,mco)]=default(%0/D`ACCOUNT,if(%1,,%0))

&ALTS [u(cobj,mco)]=u(filter,ISOBJID,u(strfirstof,get(u(accid,%0)/CHARACTERS),objid(%0)))
&PALTS [u(cobj,mco)]=alts(%@)

&ISGUEST [u(cobj,mco)]=hasflag(%0,GUEST)

&ISADMIN [u(cobj,mco)]=cor(get(%0/D`STAFF),gte(bittype(%0),2))

&ISADMIN2 [u(cobj,mco)]=u(isadmin,%#)

&ISWIZARD2 [u(cobj,mco)]=u(iswizard,%#)

&ISWIZARD [u(cobj,mco)]=gte(bittype(%0),6)

&ISHIDDEN [u(cobj,mco)]=ishidden(%0)

&ISIC [u(cobj,mco)]=default(room(%0)/D`IC,0)

&MASTER_ROOM [u(cobj,mco)]=globalroom()
&OPEN_CREATION [u(cobj,mco)]=!member(config(register_host),*)

&NATTR [u(cobj,mco)]=first(lattr(%0,,l,,,1))

&NCHILDREN [u(cobj,mco)]=last(children(%0,l))

&FILTER [u(cobj,mco)]=filter(FIL`%0,%1,if(!strlen(%2),%b,%2),if(!strlen(%3),%b,%3),%4,%5,%6,%7,%8,%9)

&CSECS [u(cobj,mco)]=convtime(createtime(%0))

&CTIME [u(cobj,mco)]=createtime(%0)

&FANCYTIME [u(cobj,mco)]=timefmt($b $D $02H:$02T$P $i,u(strfirstof,%0,secs()),u(strfirstof,%2,u(gettz,%1),u(gettz,%#)))

&FANCYDAY [u(cobj,mco)]=timefmt($b $D $Y,u(strfirstof,%0,secs()),u(gettz,u(strfirstof,%1,%#)))

&PLAYER_NAME_LEN [u(cobj,mco)]=22

&DATE [u(cobj,mco)]=timefmt($M/$D/$Y,u(strfirstof,%0,secs()),u(gettz,u(strfirstof,%1,%#)))

&CHANCOLOR [u(cobj,mco)]=localize(if(isdbref(setr(mog,cmogrifier(stripansi(%0)))),u(color,%@,CHANNEL,%q<mog>,,1)))

&GETTZ [u(cobj,mco)]=u(pconf,%0,TIMEZONE)

&CHARSEARCH [u(cobj,mco)]=u(lmax,iter(%1,strmatch(%0,*%i0*)))

&NEWCONF [u(cobj,mco)]=[u(attrib_set,u(cobj,%0),CATEGORIES,setunion(get(u(cobj,%0)/CATEGORIES),%1,|,|))][u(attrib_set,u(cobj,%0),CATEGORIES`%1,setunion(get(u(cobj,%0)/CATEGORIES`%1),%2,|,|))][u(attrib_set,u(cobj,%0),DEFAULT`%1`%2,%3)][u(attrib_set,u(cobj,%0),DESC`%1`%2,%4)][u(attrib_set,u(cobj,%0),TYPE`%1`%2,%5)]

&REMCONF [u(cobj,mco)]=@attach %!/WIPE=u(cobj,%0),*`%1`%2;&CATEGORIES`%1 [u(cobj,%0)]=setdiff(get(u(cobj,%0)/CATEGORIES`%1),%2,|,|);@select/inline words(get(u(cobj,%0)/CATEGORIES`%1),|)=0,{&CATEGORIES [u(cobj,%0)]=setdiff(get(u(cobj,%0)/CATEGORIES),%1,|,|)}

&LMAX [u(cobj,mco)]=lmax(%0,%1)

&LMIN [u(cobj,mco)]=lmin(%0,%1)

&LADD [u(cobj,mco)]=ladd(%0,%1)

&AGO [u(cobj,mco)]=singletime(%0)

&SMALLTIME [u(cobj,mco)]=singletime(%0)

&ETIME [u(cobj,mco)]=[trim(timefmt($!mML $!2UC $!2uy $!2EMo $!2ww $!2dd $!2Xh $!2Fm $!2Gs,%0))][ifelse(!t(%0),0s)]

&CAPNAMES [u(cobj,mco)]=regeditalli(lcstr(%0),v(REG`CAPNAMES),capstr($1),v(REG`CAPNAMES3),lcstr($0),v(REG`CAPNAMES2),$1[capstr($2)])

&REG`CAPNAMES [u(cobj,mco)]=(?:^|(?<=[_\/\-\|\s()\+]))([a-z]+)
&REG`CAPNAMES2 [u(cobj,mco)]=(^|(?<=[(\|\/]))(of|the|a|and|in)
&REG`CAPNAMES3 [u(cobj,mco)]=\b(of|the|a|and|in)\b

&COMMAFY [u(cobj,mco)]=squish(flip(foreach(#lambda/if(mod([lit(%1)],3),[lit(%0)],\\,[lit(%0)]),flip(%0))),\,)

@@ Maybe universal?
@@ [switch(flip(foreach(#lambda/[lit([if(mod(%1,3),%0,%,%0)])],flip(%0))),%,)]

&SETSTAT [u(cobj,mco)]=localize(u(delstat,%0,%1,%2)[u(attrib_set,%0,%1,setunion(get(%0/%1),%2~%3,|))])
&DELSTAT [u(cobj,mco)]=u(attrib_set,%0,%1,setdiff(get(%0/%1),grab(get(%0/%1),%2~*,|),|))
&GETSTAT [u(cobj,mco)]=if(strlen(%3),add(rest(grab(get(%0/%1),%2~*,|),~),rest(grab(get(%0/D`%3),%2~*,|),~)),rest(grab(get(%0/%1),%2~*,|),~))
&GRABSTAT [u(cobj,mco)]=rest(grab(get(%0/%1),%2~*,|),~)

&LISTMAX [u(cobj,mco)]=u(lmax,iter(%0,strlen(%i0),|,|),|)

&NEXTSLOT [u(cobj,mco)]=add(1,u(lmax,iter(u(filter,ISSLOT,u(lattr,%0/[if(strlen(%1),%1`*,*)])),last(%i0,`))))

&MONIKER [u(cobj,mco)]=cname(%0)

&NAMELINK [u(cobj,mco)]=name(%0)

&NAMEGRAB [u(cobj,mco)]=first(trim(squish(iter(%0,if(cor(strmatch(name(%i0),*%1*),strmatch(%i0,%1),strmatch(u(getalias,%i0),%i0)),%i0)))))

&NAMEGRABALL [u(cobj,mco)]=trim(squish(iter(%0,if(cor(strmatch(name(%i0),*%1*),strmatch(%i0,%1),strmatch(u(getalias,%i0),%i0)),%i0))))

&NUMTH [u(cobj,mco)]=%0[switch(%0,11,th,12,th,13,th,switch(right(%0,1),1,st,2,nd,3,rd,th))]

&PUEBLIZE [u(cobj,mco)]=%0

&MXPMENU [u(cobj,mco)]=%0

&CAN_MXP [u(cobj,mco)]=0
&CAN_PUEBLO [u(cobj,mco)]=0

&TIMESTAMP [u(cobj,mco)]=ptimefmt($Y-$m-$d $H:$M:$S,u(firstof,%0,secs()))

&UNPRIV [u(cobj,mco)]=localize(u(setq,unprivtext,%0)[objeval(u(cobj,ap),s(%q<unprivtext>))])

&VALNUM [u(cobj,mco)]=cand(isint(%0),gt(%0,0))

&WEBLINK [u(cobj,mco)]=%0

&RYG [u(cobj,mco)]=<[if(gt(%0,50),255,round(mul(255,fdiv(mul(%0,2),100)),0))] [if(gte(%0,50),sub(mul(255,2),round(mul(255,fdiv(mul(%0,2),100)),0)),255)] 0>

&ALIASGRAB [u(cobj,mco)]=if(match(%0,%1),%1,first(u(filter,ALIASMATCH,u(sortalias,%0),%b,%b,%1)))

&ALIASGRABALL [u(cobj,mco)]=if(match(%0,%1),%1,u(filter,ALIASMATCH,u(sortalias,%0),%b,%b,%1))

&RANDWORD [u(cobj,mco)]=randextract(%0,1,%1)

&STRFIRSTOF [u(cobj,mco)]=ofparse(5,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&FIRSTOF [u(cobj,mco)]=ofparse(1,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&ITEMIZE [u(cobj,mco)]=elist(%0,%2,%1,%4,%3)

&TABLE [u(cobj,mco)]=columns(%0,switch([gt(words(%1),0)][gt(words(%2),0)],11,min(%1,%2),01,max(10,%2),*,10),switch([gt(words(%1),0)][gt(words(%2),0)],11,max(1,div(%2,%1)),10,max(1,div(78,%1)),01,max(1,div(%2,10)),*,7),L,0,1,,ifelse(strlen(%4),%4,%b),,1,%3)

@@ Rhost Functions
&ALIGN [u(cobj,mco)]=[printf([iter(%0,$[case(mid(%i0,0,1),<,-[delete(%i0,0,1)],>,[delete(%i0,0,1)],_,_[delete(%i0,0,1)],-,^[delete(%i0,0,1)],=,_[delete(%i0,0,1)],-%i0)]|"s)],%1,%2,%3,%4,%5,%6,%7,%8,%9)]
&SORTKEY [u(cobj,mco)]=munge(#lambda/%[sort%(%%0%,%2%,%3%)%],iter(%1,edit(u(%0,itext(0)),%b,),ifelse(!$v(3),%b,%3),ifelse(!$v(3),%b,%3)),%1,ifelse(!$v(3),%b,%3),ifelse(!$v(4),ifelse(!$v(3),%b,%3),%4))
&LALIGN [u(cobj,mco)]=[strfunc(printf,[iter(%0,$[case(mid(%i0,0,1),<,-[delete(%i0,0,1)],>,[delete(%i0,0,1)],_,_[delete(%i0,0,1)],-,^[delete(%i0,0,1)],=,_[delete(%i0,0,1)],-%i0)]|"s)]%2%1,%2)]

&BASECONV [u(cobj,mco)]=pack(unpack(%0,%1),%2)

&WIDTH [u(cobj,mco)]=78

&CENTER [u(cobj,mco)]=printf($^[u(strfirstof,%1,78)]:%2:s,%0)

&HASATTRVAL [u(cobj,mco)]=!!$get(%0)

&NAMELIST [u(cobj,mco)]=iter(%0,ifelse(!match(setr(0,pmatch(##)),#-1*),%q0,#-1[objeval(%#,u(%1,##,#-1))]))

&LWHOID [u(cobj,mco)]=lwho(0,%0,1)

&LWHO [u(cobj,mco)]=lwho(0,%0)

&GETALIAS [u(cobj,mco)]=switch(type(%0),EXIT,elements(fullname(%0),2,;),get(%0/ALIAS))

&PUEBLO [u(cobj,mco)]=0

&DECOMPOSE [u(cobj,mco)]=translate(%0,p)

&LVPLAYERS [u(cobj,mco)]=lcon(%0/CONNECT)

&LPLAYERS [u(cobj,mco)]=lcon(%0/PLAYER)

&LTHINGS [u(cobj,mco)]=lcon(%0/OBJECT)

&LVTHINGS [u(cobj,mco)]=lcon(%0/OBJECT)

&LVEXITS [u(cobj,mco)]=lexits(%0)

&TRIMLINES [u(cobj,mco)]=trim(%0,b,%R)

&TRIMTABS [u(cobj,mco)]=trim(%0,b,%T)

&TRIMCASH [u(cobj,mco)]=trim(%0,b,$)

&WILDGREPI [u(cobj,mco)]=u(filter,WILDGREPI,u(lattr,%0/%1),%b,%b,%0,%2)
&FIL`WILDGREPI [u(cobj,mco)]=strmatch(get(%1/%0),%2)

&UMAIL [u(cobj,mco)]=elements(mailquick(%0,,1),2)

&FULLALIAS [u(cobj,mco)]=get(%0/ALIAS)

@@ PROPERTY SECTION
&GETPROPERTY [u(cobj,mco)]=localize(if(isdbref(%0),u(GETPROPERTY`%1,%0,%2,%3,%4)))

&GETPROPERTY`SEX [u(cobj,mco)]=get(%0/SEX)
&GETPROPERTY`NAME [u(cobj,mco)]=name(%0)
&GETPROPERTY`FULLALIAS [u(cobj,mco)]=u(fullalias,%0)
&GETPROPERTY`MONIKER [u(cobj,mco)]=u(getmoniker,%0)
&GETPROPERTY`ALIASNAME [u(cobj,mco)]=name(%0)[if(strlen(u(getalias,%0)),%B\([u(getalias,%0)]\))]
&GETPROPERTY`ALIASMONIKER [u(cobj,mco)]=u(getmoniker,%0)[if(strlen(u(getalias,%0)),%B\([u(getalias,%0)]\))]
&GETPROPERTY`ALIASMONIKERAPPROVED [u(cobj,mco)]=u(getmoniker,%0)[if(strlen(u(getalias,%0)),%B\([u(getalias,%0)]\))][if(not(u(approved,%0)),%b- [ansi(hr,UNAPPROVED)])]
&GETPROPERTY`NAMELINK [u(cobj,mco)]=u(namelink,%0)
&GETPROPERTY`HIDEIDLE [u(cobj,mco)]=u(hideidle,%0)
&GETPROPERTY`HIDECONN [u(cobj,mco)]=u(hideconn,%0)
&GETPROPERTY`SEXLETTER [u(cobj,mco)]=left(get(%0/SEX),1)
&GETPROPERTY`LOCATION [u(cobj,mco)]=if(findable(%#,%0),u(getmoniker,loc(%0)),*UNFINDABLE*)
&GETPROPERTY`JOINLOCATION [u(cobj,mco)]=if(findable(%#,%0),if(elock(loc(%0)/Teleport,%#),u(pueblize,u(getmoniker,loc(%0)),+port [loc(%0)]),u(getmoniker,loc(%0))),*UNFINDABLE*)
&GETPROPERTY`TIMEZONE [u(cobj,mco)]=u(gettz,%0)

&GETPROPERTY`PICTURE [u(cobj,mco)]=u(weblink,get(%0/D`FINGER`PICTURE))
&GETPROPERTY`WIKI [u(cobj,mco)]=u(weblink,get(%0/D`FINGER`WIKI))
&GETPROPERTY`GROUPNAMES2 [u(cobj,mco)]=u(getproperty`groupnames,%0,1)
&GETPROPERTY`FACTION2 [u(cobj,mco)]=u(getproperty`faction,%0,1)
&GETPROPERTY`MAIL [u(cobj,mco)]=u(umail,%0) Unread Messages
&GETPROPERTY`ALIAS [u(cobj,mco)]=u(getalias,%0)
&GETPROPERTY`LAST [u(cobj,mco)]=u(lastidle,%0)
&GETPROPERTY`EMAIL [u(cobj,mco)]=if(strmatch(u(setr,email,get(%0/D`OOCFINGER`EMAIL)),!*),if(u(isadmin,%#),ansi(hx,after(%q<email>,!)),*HIDDEN*),%q<email>)
&GETPROPERTY`ACCEMAIL [u(cobj,mco)]=if(isdbref(u(accid,%0)),get(u(accid,%0)/EMAIL))
&GETPROPERTY`ACCALTS [u(cobj,mco)]=if(isdbref(u(cobj,ams)),u(u(cobj,ams)/FUN`ALTS`LIST,%0))
&GETPROPERTY`THEME [u(cobj,mco)]=if(u(setr,theme,first(get(%0/D`THEME))),u(pueblize,u(setr,mon,u(getmoniker,%q<theme>)),+fclist %q<mon>))
&GETPROPERTY`TYPE [u(cobj,mco)]=default(%0/D`FINGER`TYPE,??)
&GETPROPERTY`EMAILSET [u(cobj,mco)]=if(not(u(setr,acc,u(accid,%0,1))),pemit(%0,You do not have an Account!)0,if(not(get(%q<acc>/EMAIL)),pemit(%0,You have no account email set! Use +account/email <email> to correct this.)0,1))
&GETPROPERTY`OBSERVER [u(cobj,mco)]=default(%0/D`OBSERVER,0)
&GETPROPERTY`CURSCENE [u(cobj,mco)]=if(u(getproperty`observer,%0),ansi(hw,OBS),default(%0/D`SCENE,???))

&GETPROPERTY`BANNER [u(cobj,mco)]=localize(if(cand(isdbref(u(setr,maj,u(getproperty`majordb,%0))),strlen(u(setr,col,u(getproperty`groupcolor,%q<maj>)))),ansi(%q<col>,name(%0)),u(getmoniker,%0)))

&GETPROPERTY`GROUPS [u(cobj,mco)]=localize(default(%0/D`CACHE`GROUPS,u(setr,cache,u(sortgroups,u(sortname,u(filter,NOTLURK,setinter(get(%0/D`GROUPS),u(u(cobj,gms)/FUN`LISTGROUPS)),%b,%b,%0))))[u(attrib_set,%0,D`CACHE`GROUPS,%q<cache>)]))

&FIL`NOTLURK [u(cobj,mco)]=not(strmatch(get(%1/D`GROUP`%0`RANK),LURK))

&GETPROPERTY`MAJORDBS [u(cobj,mco)]=localize(default(%0/D`CACHE`MAJORDBS,u(setr,cache,u(sortgroups,u(sortname,u(filter,MAJOR,u(GETPROPERTY`GROUPS,%0)))))[u(attrib_set,%0,D`CACHE`MAJORDBS,%q<cache>)]))
&GETPROPERTY`MAJORDB [u(cobj,mco)]=first(u(GETPROPERTY`MAJORDBS,%0))
&GETPROPERTY`MINORDBS [u(cobj,mco)]=localize(default(%0/D`CACHE`MINORDBS,u(setr,cache,u(sortgroups,u(sortname,u(filter,MINOR,u(GETPROPERTY`GROUPS,%0)))))[u(attrib_set,%0,D`CACHE`MINORDBS,%q<cache>)]))
&GETPROPERTY`MINORDB [u(cobj,mco)]=first(u(GETPROPERTY`MINORDBS,%0))
&GETPROPERTY`FIRSTDB [u(cobj,mco)]=u(strfirstof,u(GETPROPERTY`MAJORDB,%0),u(GETPROPERTY`MINORDB,%0))


&GETPROPERTY`GROUPNAMES [u(cobj,mco)]=iter(u(sortgroups,u(sortname,setdiff(u(getproperty`groups,%0),u(getproperty,%0,MAJORDB)))),u(getproperty,%i0,GROUPNAME,1),%b,if(%1,\,%B,|))

&GETPROPERTY`GROUPNAME [u(cobj,mco)]=localize(u(setq,gname,ansi(u(GETPROPERTY`GROUPCOLOR,%0),u(getmoniker,%0)))[if(%1,u(pueblize,%q<gname>,+group [name(%0)]),%q<gname>)])
&GETPROPERTY`GROUPCOLOR [u(cobj,mco)]=u(strfirstof,u(color,%#,GROUP,%0,,1),get(%0/SET`COLOR),n)
&GETPROPERTY`GROUPABBR [u(cobj,mco)]=ansi(u(GETPROPERTY`GROUPCOLOR,%0),default(%0/SET`ABBREVIATION,left(name(%0),3)))
&GETPROPERTY`GROUPLETTER [u(cobj,mco)]=ansi(u(GETPROPERTY`GROUPCOLOR,%0),default(%0/SET`LETTER,u(strfirstof,left(get(%0/SET`ABBREVIATION),1),left(name(%0),1))))

&GETPROPERTY`RANK [u(cobj,mco)]=localize(u(setr,rank,get(%0/D`GROUP`%1`RANK))-[u(strfirstof,get(%0/D`GROUP`%1`TITLE),get(%1/RANK`%q<rank>`NAME))])

&GETPROPERTY`MAJORNAME [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`MAJORDB,%0))),u(GETPROPERTY`GROUPNAME,%q<fac>,%1)))
&GETPROPERTY`MAJORNAMECLICK [u(cobj,mco)]=u(GETPROPERTY`MAJORNAME,%0,1)
&GETPROPERTY`MAJORABBR [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEM,WIZTAG),WIZ),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),ROY),if(isdbref(u(setr,fac,u(GETPROPERTY`MAJORDB,%0))),u(GETPROPERTY`GROUPABBR,%q<fac>))))
&GETPROPERTY`MAJORLETTER [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEm,WIZTAG),W),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),R),if(isdbref(u(setr,fac,u(GETPROPERTY`MAJORDB,%0))),u(GETPROPERTY`GROUPLETTER,%q<fac>))))
&GETPROPERTY`MAJORRANK [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`MAJORDB,%0))),u(GETPROPERTY`RANK,%0,%q<fac>)))
&GETPROPERTY`MAJORNAMELIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`MAJORDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`GROUPNAME,%i0,%1),%b,beep()),beep(),if(%2,%R,\,%b))))
&GETPROPERTY`MAJORNAMELISTLINE [u(cobj,mco)]=u(GETPROPERTY`MAJORNAMELIST,%0,0,1)
&GETPROPERTY`MAJORNAMELISTCLICK [u(cobj,mco)]=u(GETPROPERTY`MAJORNAMELIST,%0,1,0)
&GETPROPERTY`MAJORNAMELISTLINECLICK [u(cobj,mco)]=u(GETPROPERTY`MAJORNAMELIST,%0,1,1)
&GETPROPERTY`MAJORRANKLIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`MAJORDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`RANK,%0,%i0),%b,beep()),beep(),if(%1,%R,\,%b))))
&GETPROPERTY`MAJORRANKLISTLINE [u(cobj,mco)]=u(GETPROPERTY`MAJORRANKLIST,%0,1)

&GETPROPERTY`MINORNAME [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`MINORDB,%0))),u(GETPROPERTY`GROUPNAME,%q<fac>,%1)))
&GETPROPERTY`MINORNAMECLICK [u(cobj,mco)]=u(GETPROPERTY`MINORNAME,%0,1)
&GETPROPERTY`MINORABBR [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEM,WIZTAG),WIZ),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),ROY),if(isdbref(u(setr,fac,u(GETPROPERTY`MINORDB,%0))),u(GETPROPERTY`GROUPABBR,%q<fac>))))
&GETPROPERTY`MINORLETTER [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEm,WIZTAG),W),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),R),if(isdbref(u(setr,fac,u(GETPROPERTY`MINORDB,%0))),u(GETPROPERTY`GROUPLETTER,%q<fac>))))
&GETPROPERTY`MINORRANK [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`MINORDB,%0))),u(GETPROPERTY`RANK,%0,%q<fac>)))
&GETPROPERTY`MINORNAMELIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`MINORDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`GROUPNAME,%i0,%1),%b,beep()),beep(),if(%2,%R,\,%b))))
&GETPROPERTY`MINORNAMELISTLINE [u(cobj,mco)]=u(GETPROPERTY`MINORNAMELIST,%0,0,1)
&GETPROPERTY`MINORNAMELISTCLICK [u(cobj,mco)]=u(GETPROPERTY`MINORNAMELIST,%0,1,0)
&GETPROPERTY`MINORNAMELISTLINECLICK [u(cobj,mco)]=u(GETPROPERTY`MINORNAMELIST,%0,1,1)
&GETPROPERTY`MINORRANKLIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`MINORDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`RANK,%0,%i0),%b,beep()),beep(),if(%1,%R,\,%b))))
&GETPROPERTY`MINORRANKLISTLINE [u(cobj,mco)]=u(GETPROPERTY`MINORRANKLIST,%0,1)

&GETPROPERTY`FIRSTNAME [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`FIRSTDB,%0))),u(GETPROPERTY`GROUPNAME,%q<fac>,%1)))
&GETPROPERTY`FIRSTNAMECLICK [u(cobj,mco)]=u(GETPROPERTY`FIRSTNAME,%0,1)
&GETPROPERTY`FIRSTABBR [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEM,WIZTAG),WIZ),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),ROY),if(isdbref(u(setr,fac,u(GETPROPERTY`FIRSTDB,%0))),u(GETPROPERTY`GROUPABBR,%q<fac>))))
&GETPROPERTY`FIRSTLETTER [u(cobj,mco)]=localize(switch(1,u(iswizard,%0),ansi(u(game_config,SYSTEm,WIZTAG),W),u(isadmin,%0),ansi(u(game_config,SYSTEM,ROYTAG),R),if(isdbref(u(setr,fac,u(GETPROPERTY`FIRSTDB,%0))),u(GETPROPERTY`GROUPLETTER,%q<fac>))))
&GETPROPERTY`FIRSTRANK [u(cobj,mco)]=localize(if(isdbref(u(setr,fac,u(GETPROPERTY`FIRSTDB,%0))),u(GETPROPERTY`RANK,%0,%q<fac>)))
&GETPROPERTY`FIRSTNAMELIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`FIRSTDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`GROUPNAME,%i0,%1),%b,beep()),beep(),if(%2,%R,\,%b))))
&GETPROPERTY`FIRSTNAMELISTLINE [u(cobj,mco)]=u(GETPROPERTY`FIRSTNAMELIST,%0,0,1)
&GETPROPERTY`FIRSTNAMELISTCLICK [u(cobj,mco)]=u(GETPROPERTY`FIRSTNAMELIST,%0,1,0)
&GETPROPERTY`FIRSTNAMELISTLINECLICK [u(cobj,mco)]=u(GETPROPERTY`FIRSTNAMELIST,%0,1,1)
&GETPROPERTY`FIRSTRANKLIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`FIRSTDBS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`RANK,%0,%i0),%b,beep()),beep(),if(%1,%R,\,%b))))
&GETPROPERTY`FIRSTRANKLISTLINE [u(cobj,mco)]=u(GETPROPERTY`FIRSTRANKLIST,%0,1)

&GETPROPERTY`ALLNAMELIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`GROUPS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`GROUPNAME,%i0,%1),%b,beep()),beep(),if(%2,%R,\,%b))))
&GETPROPERTY`ALLNAMELISTLINE [u(cobj,mco)]=u(GETPROPERTY`ALLNAMELIST,%0,0,1)
&GETPROPERTY`ALLNAMELISTCLICK [u(cobj,mco)]=u(GETPROPERTY`ALLNAMELIST,%0,1,0)
&GETPROPERTY`ALLNAMELISTLINECLICK [u(cobj,mco)]=u(GETPROPERTY`ALLNAMELIST,%0,1,1)
&GETPROPERTY`ALLRANKLIST [u(cobj,mco)]=localize(if(words(u(setr,facs,u(GETPROPERTY`GROUPS,%0))),edit(iter(%q<facs>,u(GETPROPERTY`RANK,%0,%i0),%b,beep()),beep(),if(%1,%R,\,%b))))
&GETPROPERTY`ALLRANKLISTLINE [u(cobj,mco)]=u(GETPROPERTY`ALLRANKLIST,%0,1)

&GETPROPERTY`MAJORDIV [u(cobj,mco)]=if(isdbref(u(setr,mdb,u(GETPROPERTY`MAJORDB,%0))),if(u(nattr,%0/D`GROUP`%q<mdb>`DIVISION`*`RANK),if(u(setr,div,localize([u(setq,gid1,%q<mdb>)][last(first(u(filter,INDIV,u(u(cobj,gms)/SORTDIV,u(lattr,%q<mdb>/DIVISION`*)),%b,%b,%q<gid1>,objid(%0))),`)])),%q<div>)))
&GETPROPERTY`MAJORDIVNAME [u(cobj,mco)]=if(u(getproperty`majordiv,%0),get(%q<mdb>/DIVISION`%q<div>))
&GETPROPERTY`MAJORDIVRANK [u(cobj,mco)]=if(u(getproperty`majordiv,%0),get(%q<mdb>/DIVISION`%q<div>`RANK`[get(%0/D`GROUP`%q<mdb>`DIVISION`%q<div>`RANK)]))
&GETPROPERTY`MAJORDIVRANKNUM [u(cobj,mco)]=if(u(getproperty`majordiv,%0),get(%0/D`GROUP`%q<mdb>`DIVISION`%q<div>`RANK))

&SORTGROUPS [u(cobj,mco)]=localize(sortby(#lambda/[lit([ncomp(default(%0/SET`SORT,99),default(%1/SET`SORT,99))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B)))

&FIL`MAJOR [u(cobj,mco)]=t(u(u(cobj,gms)/FUN`GETSET,%0,MAJOR))
&FIL`MINOR [u(cobj,mco)]=not(u(u(cobj,gms)/FUN`GETSET,%0,MAJOR))

&FIL`INDIV [u(cobj,mco)]=hasattr(%2/D`GROUP`%1`%0`RANK)

@@ cand(u(u(cobj,gms)/FUN`GETSET,%0,MAJOR),strmatch(FACTION,u(u(cobj,gms)/FUN`GETSET,%0,TYPE)))
&FIL`MAJORGROUPS [u(cobj,mco)]=u(u(cobj,gms)/FUN`GETSET,%0,MAJOR)
&FIL`MINORGROUPS [u(cobj,mco)]=eq(u(u(cobj,gms)/FUN`GETSET,%0,MAJOR),0)

&ISGROUPMEMBER [u(cobj,mco)]=if(isdbref(u(cobj,gms)),elock(if(isdbref(%1),%1,u(u(cobj,gms)/FUN`FINDGROUP,%1)),%0))

&chan_title_len [u(cobj,mco)]=u(choosegame,config(chan_title_len),20)

&CODENAME [u(cobj,mco)]=if(match(get(%0/MOGUSE`PLAYERNAME),codename),get(%0/CODENAME`%#))

@@ Sort section.

&SORTATTR [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(last(%0,`),last(%1,`))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&SORTIDLE [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(idle(%0),idle(%1))])],%0,if(strlen(%1),%1,%B),%2)

&SORTNAME [u(cobj,mco)]=sortby(#lambda/[lit([comp(lcstr(name(%0)),lcstr(name(%1)))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&SORTALIAS [u(cobj,mco)]=sortby(#lambda/[lit([comp(lcstr(u(alias,%0)),lcstr(u(alias,%1)))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&SORTOLDID [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(after(%0,~),after(%1,~))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&SORTPRIORITY [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(default(%0/D`ALTS`PRIORITY,0),default(%1/D`ALTS`PRIORITY,0))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&SORTORDER [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(default(%0/ORDER,99),default(%1/ORDER,99))])],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))

&CHOOSEGAME [u(cobj,mco)]=%1

@@ REGEX STORAGE
&REG`CAPNAMES [u(cobj,mco)]=(?:^|(?<=[_\/\-\|\s()\+]))([a-z]+)
&REG`CAPNAMES2 [u(cobj,mco)]=(^|(?<=[(]))(of|the|a|and)
&REG`CAPNAMES3 [u(cobj,mco)]=\b(of|the|a|and)\b

&REG`SQL`ENTRY [u(cobj,mco)]=\?|\!

@@ SQL WRAPPER

&MYSQL [u(cobj,mco)]=if(hasattrp(%!/Q`%0),sql(u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-1 SQL QUERY DATA NOT FOUND)
&MYSQL2 [u(cobj,mco)]=if(hasattrp(%!/Q`%0),sql(u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9),|,^),#-1 SQL QUERY DATA NOT FOUND)
&MYSQL3 [u(cobj,mco)]=if(hasattrp(%!/Q`%0),sql(u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9),chr(176),chr(177)),#-1 SQL QUERY DATA NOT FOUND)

&CALL`0 [u(cobj,mco)]=sql(CALL %0(),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`1 [u(cobj,mco)]=sql(CALL %0(%2),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`2 [u(cobj,mco)]=sql(CALL %0(%2,%3),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`3 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`4 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4,%5),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`5 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4,%5,%6),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`6 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4,%5,%6,%7),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`7 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4,%5,%6,%7,%8),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&CALL`8 [u(cobj,mco)]=sql(CALL %0(%2,%3,%4,%5,%6,%7,%8,%9),switch(%1,0,%b,2,|,3,chr(176),%b),switch(%1,0,%b,2,^,3,chr(177),%b))

&SQL`IN`NUMBER [u(cobj,mco)]=iter(%0,%i0,%1,\,%b)
&SQL`IN`STRING [u(cobj,mco)]=iter(%0,'[sqlescape(%i0)]',%1,\,%b)

&SQL`FORMAT [u(cobj,mco)]=localize(regeditalli(v(Q`%0),v(REG`SQL`ENTRY),if(isint(u(setr,set,v(u(setr,num,add(%q<num>,1))))),%q<set>,switch($0,!,%q<set>,'[sqlescape(%q<set>)]'))))

&INC`DOSQL [u(cobj,mco)]=@stop strmatch(u(setr,errcheck,u(mysql,before(%0,/),%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),u(setq,after(%0,/),sql(SELECT last_insert_id\(\))))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`DOCALL`0 [u(cobj,mco)]=@stop strmatch(u(setr,errcheck,u(call`0,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),u(setq,after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`DOCALL`1 [u(cobj,mco)]=@stop strmatch(u(setr,errcheck,u(call`1,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),u(setq,after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

&INC`DOCALL`2 [u(cobj,mco)]=@stop strmatch(u(setr,errcheck,u(call`2,before(%0,/),0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)[if(strlen(after(%0,/)),u(setq,after(%0,/),first(%q<errcheck>)))]={@attach %!/INC`MSG=SQL ERROR - [before(%0,/)]: %q<errcheck>;@attach %!/INC`MSG`CHAN=SQL ERROR - %!/[before(%0,/)]: %q<errcheck>}

@@ The CONFIG Subsystem.
&CMD`+GAMECONFIG`RHOSTMUSH [u(cobj,mco)]=$^\+gameconfig(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,mco)]/CMD`+GAMECONFIG`RHOSTMUSH=regexp no_inherit
&CMD`+GAMECONFIG`MAIN [u(cobj,mco)]=@attach %!/INC`CONFIG=%2,%3

&INC`CONFIG [u(cobj,mco)]=@select/inline strlen(%0)=0,{@attach %!/INC`CONFIG`DISPLAY},{@check u(iswizard,%#)=@attach %!/INC`MSG=ERROR: Permission denied. Changing Config requires Wizard permissions.;@attach %!/INC`CONFIG`CHANGE}
&INC`CONFIG`DISPLAY [u(cobj,mco)]=@pemit %#=u(header,u(SYSTEM`NAME) Configuration);@dolist/inline/delimit | [v(CONFIG`OPTIONS)]={@pemit %#=u(separator,##);@pemit %#=align(10 10 12 40,rjust(ansi(hw,TYPE:),10)%R[rjust(ansi(hw,DEFAULT:),10)],v(CONFIG`##`VALID)%R[v(CONFIG`##`DEFAULT)],ansi(hw,DESCRIPTION:),v(CONFIG`##));@pemit %#=[rjust(ansi(hw,CURRENT:),10)]%b[u(conf,##)]};@pemit %#=u(footer)
&INC`CONFIG`CHANGE [u(cobj,mco)]=@attach %!/INC`PARTIAL=%0,v(CONFIG`OPTIONS),|,op,Option;@select/inline strlen(%1)=>0,{@attach %!/INC`CONFIG`SET},{@attach %!/INC`CONFIG`CLEAR}
&INC`CONFIG`SET [u(cobj,mco)]=@attach %!/INC`VALID`[v(CONFIG`%q<op>`VALID)]=%1;th u(attrib_set,%!,CONFIG`%q<op>`CUSTOM,%q<value>);@attach %!/INC`MSG=You set Config Option '%q<op>' to: %q<value>;@attach %!/INC`MSG`CHAN=Config Option '%q<op>' set to: %q<value>;@attach %!/CONFIG`%q<op>`FINISH
&INC`CONFIG`CLEAR [u(cobj,mco)]=@attach %!/WIPE=%!,CONFIG`%q<op>`CUSTOM;@attach %!/INC`MSG=You set Config Option '%q<op>' to its Default.;@attach %!/INC`MSG`CHAN=Config Option '%q<op>' reset to Default.;@attach %!/CONFIG`%q<op>`FINISH

&CONFIG`OPTIONS [u(cobj,mco)]=WIZTAG|ROYTAG|ANONYMOUS_NOTICES|PUBLIC_EMAIL|STAFF_ALERTS|HEADER_FILL|SUBHEADER_FILL|SEPARATOR_FILL|FOOTER_FILL|BORDER_MODE|COLUMN_NAMES|HEADER_TEXT|HEADER_STAR|BORDER|MSG|MSGTEXT|STAFF_GROUP
@set [u(cobj,mco)]/CONFIG`OPTIONS=no_inherit

&CONFIG`WIZTAG [u(cobj,mco)]=Color of WIZ tag in systems.
&CONFIG`WIZTAG`DEFAULT [u(cobj,mco)]=hr
&CONFIG`WIZTAG`VALID [u(cobj,mco)]=COLOR

&CONFIG`ROYTAG [u(cobj,mco)]=Color of ROY tag in systems.
&CONFIG`ROYTAG`DEFAULT [u(cobj,mco)]=hr
&CONFIG`ROYTAG`VALID [u(cobj,mco)]=COLOR

&CONFIG`STAFF_GROUP [u(cobj,mco)]=Staff Group for use with wizlist, +admin, etc. Recommend usage of a Private Group called _Admin
&CONFIG`STAFF_GROUP`DEFAULT [u(cobj,mco)]=
&CONFIG`STAFF_GROUP`VALID [u(cobj,mco)]=GROUP

&CONFIG`ANONYMOUS_NOTICES [u(cobj,mco)]=Anonymize administrative actions in messages to players?
&CONFIG`ANONYMOUS_NOTICES`DEFAULT [u(cobj,mco)]=0
&CONFIG`ANONYMOUS_NOTICES`VALID [u(cobj,mco)]=BOOL

&CONFIG`PUBLIC_EMAIL [u(cobj,mco)]=Game's public email for purpose of various messages.
&CONFIG`PUBLIC_EMAIL`DEFAULT [u(cobj,mco)]=
&CONFIG`PUBLIC_EMAIL`VALID [u(cobj,mco)]=WORD

&CONFIG`STAFF_ALERTS [u(cobj,mco)]=Channel(s) to send important staff-only reports to.
&CONFIG`STAFF_ALERTS`DEFAULT [u(cobj,mco)]=
&CONFIG`STAFF_ALERTS`VALID [u(cobj,mco)]=LIST

&CONFIG`HEADER_FILL [u(cobj,mco)]=Symbol to use for Header lines.
&CONFIG`HEADER_FILL`DEFAULT [u(cobj,mco)]==
&CONFIG`HEADER_FILL`VALID [u(cobj,mco)]=WORD

&CONFIG`SUBHEADER_FILL [u(cobj,mco)]=Symbol to use for Sub-Header lines.
&CONFIG`SUBHEADER_FILL`DEFAULT [u(cobj,mco)]==
&CONFIG`SUBHEADER_FILL`VALID [u(cobj,mco)]=WORD

&CONFIG`SEPARATOR_FILL [u(cobj,mco)]=Symbol to use for Separator lines.
&CONFIG`SEPARATOR_FILL`DEFAULT [u(cobj,mco)]==
&CONFIG`SEPARATOR_FILL`VALID [u(cobj,mco)]=WORD

&CONFIG`FOOTER_FILL [u(cobj,mco)]=Symbol to use for Footer lines.
&CONFIG`FOOTER_FILL`DEFAULT [u(cobj,mco)]=-
&CONFIG`FOOTER_FILL`VALID [u(cobj,mco)]=WORD

&CONFIG`BORDER_MODE [u(cobj,mco)]=Alters style of headers/footers. 0 = No edges. 1 = + edges. 2 = slash-edges
&CONFIG`BORDER_MODE`DEFAULT [u(cobj,mco)]=0
&CONFIG`BORDER_MODE`VALID [u(cobj,mco)]=WORD

&CONFIG`COLUMN_NAMES [u(cobj,mco)]=Color for table column names.
&CONFIG`COLUMN_NAMES`DEFAULT [u(cobj,mco)]=g
&CONFIG`COLUMN_NAMES`VALID [u(cobj,mco)]=COLOR

&CONFIG`HEADER_TEXT [u(cobj,mco)]=Color for text in System Header/Subheaders.
&CONFIG`HEADER_TEXT`DEFAULT [u(cobj,mco)]=hw
&CONFIG`HEADER_TEXT`VALID [u(cobj,mco)]=COLOR

&CONFIG`HEADER_STAR [u(cobj,mco)]=Color for * in System Headers.
&CONFIG`HEADER_STAR`DEFAULT [u(cobj,mco)]=hm
&CONFIG`HEADER_STAR`VALID [u(cobj,mco)]=COLOR

&CONFIG`BORDER [u(cobj,mco)]=Color for borders in enclosed tables.
&CONFIG`BORDER`DEFAULT [u(cobj,mco)]=m
&CONFIG`BORDER`VALID [u(cobj,mco)]=COLOR

&CONFIG`MSG [u(cobj,mco)]=Color for the -=<>=- part of System Messages.
&CONFIG`MSG`DEFAULT [u(cobj,mco)]=hm
&CONFIG`MSG`VALID [u(cobj,mco)]=COLOR

&CONFIG`MSGTEXT [u(cobj,mco)]=Color for the System Name in System Messages.
&CONFIG`MSGTEXT`DEFAULT [u(cobj,mco)]=hw
&CONFIG`MSGTEXT`VALID [u(cobj,mco)]=COLOR

@@ th u(NEWCONF,config,ROOM,PLAYERS_MODE,DEFAULT,Changes room format for PLAYERS.,WORD)
@@ th u(NEWCONF,config,ROOM,THINGS_MODE,DEFAULT,Changes room format for THINGS.,WORD)
@@ th u(NEWCONF,config,ROOM,EXITS_MODE,DEFAULT,Changes room format for EXITS.,WORD)

@@ th u(NEWCONF,config,COLOR,ROOM_COLUMNS,,For column names in room formats,COLOR)
@@ th u(NEWCONF,config,COLOR,EXITNAME,,Color of exit names.,COLOR)
@@ th u(NEWCONF,config,COLOR,EXITALIAS,,Color of exit names.,COLOR)

@@ th u(NEWCONF,pconf,ROOM,ROOM_COLUMNS,,For column names in room formats,COLOR)
@@ th u(NEWCONF,pconf,ROOM,EXITNAME,,Color of exit names.,COLOR)
@@ th u(NEWCONF,pconf,ROOM,EXITALIAS,,Color of exit names.,COLOR)


&CONF [u(cobj,mco)]=u(strfirstof,get(%!/CONFIG`%0`CUSTOM),get(%!/CONFIG`%0`DEFAULT))
&PCONF [u(cobj,mco)]=u(color,%@,%1)

&COLOR [u(cobj,mco)]=u(firstof,get(%0/V`CONFIG`%1),u(conf,%1))

@@ COMMON CODE SNIPPETS

&SYSTEM`NAME [u(cobj,mco)]=SYSTEM

&INC`CHECKPC [u(cobj,mco)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: You must enter a player name!;@check strlen(%1);@check isdbref(u(setr,t%1,pmatch(%0)))=@attach %!/INC`MSG=ERROR: %0 [if(strmatch(%q<t%1>,#-2*),matches multiple players!,does not match a player!)];th u(setq,t%1objid,objid(r(t%1)));th u(setq,t%1name,name(r(t%1)));th u(setq,t%1acc,u(accid,r(t%1),1));th u(setq,t%1id,get(r(t%1)/D`ID));th u(setq,t1accid,get(r(t%1acc)/D`ID));
@@ CHECK PC Arguments: %0 = player being checked, %1 = register number to load their data into.

&INC`VERIFY [u(cobj,mco)]=@select/inline strmatch(get(%#/VERIFY_OK),%1)=0,{th u(attrib_set,%#,VERIFY_OK,%1);@wait 10=@wipe %#/VERIFY_OK;@stop 1={@attach %!/INC`MSG=%0,,,%2}},1,{@wipe %#/VERIFY_OK}
@@ VERIFY Arguments: %0 = message to display, %1 = code to set for verification.

&INC`CPTREE [u(cobj,mco)]=@cpattr %0/%1=%2/%3;@dolist/inline u(lattr,%0/%1`**)={@cpattr %0/##=%2/%3`[elements(##,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)]}

&AGFN`CPTREE [u(cobj,mco)]=null(attrib_set(%2/%3,get(%0/%1))[iter(lattr(%0/%1`**),attrib_set(%2/%3`[elements(%i0,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)],get(%0/%i0)))])

@@ CPTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`MVTREE [u(cobj,mco)]=@cpattr %0/%1=%2/%3;@dolist/inline u(lattr,%0/%1`**)={@cpattr %0/##=%2/%3`[elements(##,lnum(add(1,words(%1,`)),add(words(%1,`),11)),`,`)]};@attach %!/WIPE=%0,%1
@@ MVTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`GETSWITCH [u(cobj,mco)]=@attach %!/INC`PARTIAL=%0,u(SYSTEM`SWITCHES),|,switch,switch

&SYSTEM`SWITCHES [u(cobj,mco)]=setunion(setunion(u(SWITCHES`PLAYER),u(SWITCHES`PLAYER`EXTRA),|,|),if(u(isadmin,%#),setunion(u(SWITCHES`ADMIN),u(SWITCHES`ADMIN`EXTRA),|,|)),|,|)

&INC`PARTIAL [u(cobj,mco)]=@select/inline or(not(strlen(%0)),u(setr,matched,match(%1,u(setr,u(strfirstof,%3,choice),%0),u(strfirstof,%2,%b))))=0,{@check words(u(setr,u(strfirstof,%3,choice),graball(%1,%0*,u(strfirstof,%2,%b),u(strfirstof,%2,%b))))=@attach %!/INC`MSG={ERROR: Invalid %4! Valid choices are: [u(itemize,%1,u(strfirstof,%2,%b),and,\,)]};@stop gt(words(r(u(strfirstof,%3,choice)),u(strfirstof,%2,%b)),1)=@attach %!/INC`MSG={ERROR: %0 matched [u(itemize,r(u(strfirstof,%3,choice)),u(strfirstof,%2,%b),and,\,)]. Please be more specific.}},1,{@select/inline cand(t(strlen(%0)),t(%q<matched>))=1,{th u(setq,u(strfirstof,%3,choice),elements(%1,%q<matched>,u(strfirstof,%2,%b)))}}
@@ PARTIAL %0 = entry, %1 = choices, %2 = delimiter, %3 = register name, %4 = topic name

&WIPE [u(cobj,mco)]=@select/inline %va=PennMUSH,{@select/inline hasflag(%0,SAFE)=1,{@set %0=!SAFE;@wipe %0/%1;@set %0=SAFE},0,{@wipe %0/%1}},RhostMUSH,{@select/inline hasflag(%0,SAFE)=1,{@set %0=!SAFE;@wipe/regexp %0/^%1(`.+)?$;@set %0=SAFE},0,{@wipe/regexp %0/^%1(`.+)?$}}

&DELETE [u(cobj,mco)]=@select/inline %va=PennMUSH,{@set %0=!SAFE;@nuke %0;@nuke %0},RhostMUSH,{@destroy/override %0}

&INC`VALID`EMAIL [u(cobj,mco)]=th u(setq,value,trim(%0));@check strlen(%q<value>)=@attach %!/INC`MSG=ERROR: Nothing entered!;@check lte(strlen(%q<value>),255)=@attach %!/INC`MSG=ERROR: Emails must be 255 characters or less.;@stop strmatch(%q<value>,* *)=@attach %!/INC`MSG=ERROR: Email addresses may not contain spaces.;@check strmatch(%q<value>,*@*.*)=@attach %!/INC`MSG=ERROR: An Email must be in the format: *@*.* (example@game.com).
&INC`VALID`COLOR [u(cobj,mco)]=@stop strmatch(ansi(u(setr,value,%0),Test),#-*)=@attach %!/INC`MSG=ERROR: '%0' is not a valid color.
&INC`VALID`WORD [u(cobj,mco)]=th u(setq,value,trim(%0))
&INC`VALID`LIST [u(cobj,mco)]=th u(setq,value,trim(%0))
&INC`VALID`BOOL [u(cobj,mco)]=@check match(0 1,u(setr,value,%0))=@attach %!/INC`MSG={ERROR: BOOL options can only be 0 (false) or 1 (true).}
&INC`VALID`DBREF [u(cobj,mco)]=@check isdbref(%0)=@attach %!/INC`MSG={ERROR: DBREF not found.};th u(setq,value,objid(%0))
&INC`VALID`TZ [u(cobj,mco)]=@check valid(timezone,u(setr,value,%0))=@attach %!/INC`MSG={ERROR: That is not a valid timezone. Check help timezone}
&INC`VALID`DURATION [u(cobj,mco)]=@check u(setr,value,u(stringsecs,%0))=@attach %!/INC`MSG={ERROR: '%0' did not resolve into a time. Check help stringsecs()}
&INC`VALID`POSINT [u(cobj,mco)]=@check u(valnum,%0)=@attach %!/INC`MSG={ERROR: '[u(strfirstof,%1,%0)]' must be a whole number 1 or greater.};th u(setq,value,%0)
&INC`VALID`INT [u(cobj,mco)]=@check isint(%0)=@attach %!/INC`MSG={ERROR: '%0' must be a whole number 0 or greater.};th u(setq,value,%0)
&INC`VALID`LOCK [u(cobj,mco)]=@check valid(lockkey,%0)=@attach %!/INC`MSG={ERROR: '%0' was not accepted by the @lock system.};th u(setq,value,%0)
&INC`VALID`TIME [u(cobj,mco)]=@check strlen(%0)=@attach %!/INC`MSG=You didn't enter a date!;@check gt(u(setr,time,convtime(%0,u(gettz,%#))),0)=@attach %!/INC`MSG=The entered date was not recognized. Did you typo? Dates should be in abbreviated 24-hour <month> <day> <hour>:<minute> format using YOUR timezone\, such as Jun 26 7:00 or Oct 31 13:00.;
&INC`VALID`FUTURE [u(cobj,mco)]=@attach %!/INC`VALID`TIME;@check gt(%q<time>,secs())=@attach %!/INC`MSG=That would be in the past!
&INC`VALID`GROUP [u(cobj,mco)]=

@@ MESSAGING SECTION
&INC`MSG [u(cobj,mco)]=@pemit/list u(strfirstof,%1,%#)=udefault(%!/MSG,%0,##,%0,%1,u(SYSTEM`NAME))
@@ %0 - Message. %1 - Recipients. %2 - Format obj/attr.

&INC`MSG`ROOM [u(cobj,mco)]=@pemit/list u(strfirstof,%1,lcon(%l))=udefault(%!/RMSG,%0,##,%0,%1,u(SYSTEM`NAME),u(strfirstof,%4,%#))

&INC`MSG`NOTICE [u(cobj,mco)]=@attach %!/INC`MSG=[ansi(h,\[[if(cand(u(isadmin,%#),cor(u(ishidden,%#),u(conf,ANONYMOUS_NOTICES))),SYSTEM,name(%#))]\])]%B%0,%1,%2,%3,%4,%5,%6,%7,%8,%9

&INC`MSG`CHAN [u(cobj,mco)]=th u(setq,chanmsg,if(not(%5),ansi(h,\[[u(getmoniker,u(firstof,%4,%#))]\])%B)[if(cand(strlen(u(strfirstof,%2,u(SYSTEM`NAME))),not(%3)),u(strfirstof,%2,u(SYSTEM`NAME)):%b)]%0);@dolist/inline/delimit | [u(strfirstof,%1,u(conf,STAFF_ALERTS))]={@cemit %d0=%q<chanmsg>}
@@ %0 - Message. %1 - Channels (| delimit). %2 - System. %3 - Ignore system boolean. %4 - Enactor. %5 - Ignore enactor.

&INC`MSG`SYSCHAN [u(cobj,mco)]=th u(setq,chanmsg,[if(not(%5),ansi(h,\[[u(strfirstof,%4,SYSTEM)]\]%B))][if(cand(strlen(u(strfirstof,%2,u(SYSTEM`NAME))),not(%3)),u(strfirstof,%2,u(SYSTEM`NAME)):%b)]%0);@dolist/inline/delimit | [u(strfirstof(%1,u(conf,STAFF_ALERTS)))]={@cemit %d0=%q<chanmsg>}

&MSG [u(cobj,mco)]=u(msghead,%3,%0) %1
&RMSG [u(cobj,mco)]=u(msgroom,%3,%0) \[[ansi(h,u(getmoniker,%4))]\] %1

&MSGHEAD [u(cobj,mco)]=localize(ansi(u(setr,msgcolor,u(color,u(firstof,%1,%#),MSG)),-=<)[ansi(u(color,u(firstof,%1,%#),MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,>=-)])
&MSGROOM [u(cobj,mco)]=localize(ansi(u(setr,msgcolor,u(color,u(firstof,%1,%#),MSG)),-=<*)[ansi(u(color,u(firstof,%1,%#),MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,*>=-)])

&HEADER [u(cobj,mco)]=localize(u(HEADER`PREP,%1,u(strfirstof,%3,u(conf,BORDER_MODE)),HEADER,%4)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][u(setr,borderstar,ansi(u(color,%q<target>,HEADER_STAR),*))]%B[if(%2,%0,ansi(u(color,%q<target>,HEADER_TEXT),%0))]%B%q<borderstar>[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&SUBHEADER [u(cobj,mco)]=localize(u(HEADER`PREP,%1,%3,SUBHEADER,%4)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)]%B[if(%2,%0,ansi(u(color,%q<target>,HEADER_TEXT),%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&SEPARATOR [u(cobj,mco)]=localize(u(HEADER`PREP,%1,%3,SEPARATOR,%4)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)]%B[if(%2,%0,ansi(u(color,%q<target>,HEADER_TEXT),%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&FOOTER [u(cobj,mco)]=localize(u(HEADER`PREP,%1,u(strfirstof,%3,u(conf,BORDER_MODE)),FOOTER,%4)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)]%B[if(%2,%0,ansi(u(color,%q<target>,HEADER_TEXT),%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&HEADER`PREP [u(cobj,mco)]=u(setq,target,owner(u(firstof,%0,%#)))[u(setq,bordercolor,u(color,%q<target>,BORDER))][u(setq,bordermode,%1)][u(setq,width,sub(u(strfirstof,%3,u(width,%q<target>)),2))][u(setq,fill,u(conf,%2_FILL))][switch(%q<bordermode>,2,switch(%2,HEADER,u(setq,start,ansi(%q<bordercolor>,/))[u(setq,end,ansi(%q<bordercolor>,chr(92)))],FOOTER,u(setq,start,ansi(%q<bordercolor>,chr(92)))[u(setq,end,ansi(%q<bordercolor>,/))]),1,u(setq,start,u(setr,end,ansi(%q<bordercolor>,+))),u(setq,start,u(setr,end,ansi(%q<bordercolor>,%q<fill>))))]

&HEADER`RENDER [u(cobj,mco)]=%q<start>[u(center,%q<headertext>,%q<width>,ansi(%q<bordercolor>,%q<fill>))]%q<end>

@@ HEADER family args: %0 - Title. %1 - Viewer dbref. %2 - True: Do not color title. %3 - Border mode bool. %4 - Width.

&MARKUP [u(cobj,mco)]=localize(regeditalli(%0,\\b\([iter(%1,name(%i0),%b,|)]\)\\b,if(neq(ord(left(setr(found,$0),1)),ord(lcstr(left($0,1)))),^^^[pmatch(%q<found>)]:%q<found>^^^,%q<found>)))
@@ %0 - Text to search. %1 - Dbrefs to check.

&COLORMARKUP [u(cobj,mco)]=localize(if(%3,regeditalli(%1,v(REG`MARKUP),$2),regeditalli(%1,v(REG`MARKUP),if(strlen(setr(a,if(strlen(setr(b,u(color,%0,NAMES,$1,,1))),%qb,if(strlen(setr(c,switch($1,%0,u(color,%0,NAMES,MINE,,1),%2,u(color,%0,NAMES,SPEAKER,,1),u(color,%0,NAMES,OTHER,,1)))),%qc,)))),ansi(%qa,$2),$2))))

&CHANHASH [u(cobj,mco)]=default(cmogrifier(%0)/HASH`[digest(md5,%1)],%1)

@@ %0 - Viewer. %1 - Text to search. %2 - Speaker. %3 - Bool for 'just demarkup'

&REG`MARKUP [u(cobj,mco)]=\^\^\^(#\d+)\:([^^]+)\^\^\^

@@ &SPEECH`PENNMUSH [u(cobj,mco)]=regeditalli(speakpenn(&[if(strlen(%3),%3,u(getmoniker,%0))],u(colornames,%2,%1,%4,%5,%0)),v(REG`URL),u(weblink,$0,$0),v(REG`SPEECH),u(setr,quote,ansi(u(player_config,%2,%4,QUOTES),"))[ansi(u(player_config,%2,%4,SPEECH),$1)]%q<quote>)

&SPEECH [u(cobj,mco)]=localize(regeditalli(parsestr(u(colornames,%2,escape(%1),%4,%5,%0),%0,",",,&[if(strlen(%3),%3,u(getmoniker,%0))]),v(REG`SPEECH),u(setr,quote,ansi(u(player_config,%2,%4,QUOTES),"))[ansi(u(player_config,%2,%4,SPEECH),$1)]%q<quote>))
@@ %0 - Speaker. %1 - Message. %2 - Viewer. %3 - Name display override. %4 - Color display mode. %5 - Dbref list for colored-names lookup. %6 - Banner override bool.

&REG`SPEECH [u(cobj,mco)]=(?s)"([^ "][^"]*[^ "])"
&REG`URL [u(cobj,mco)]=\w+\://\S+[^"]

&COLORNAMES [u(cobj,mco)]=localize(u(setq,colstring,iter(%3,lit(%\b)[name(%i0)][lit(%\b)][beep()][switch(%i0,%0,if(strlen(u(setr,colfound,u(strfirstof,u(color,%0,NAMES,MINE,,1),u(color,%0,NAMES,%i0,,1)))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)),%4,if(strlen(u(setr,colfound,u(strfirstof,u(color,%0,NAMES,%i0,,1),u(color,%0,NAMES,SPEAKER,,1)))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)),if(strlen(u(setr,colfound,u(strfirstof,u(color,%0,NAMES,%i0,,1),u(color,%0,NAMES,OTHER,,1)))),ansi(%q<colfound>,name(%i0)),u(getmoniker,%i0)))],%b,beep()))[strfunc(regeditalli,%1[beep()]%q<colstring>,beep())])

@@ %0 - Viewer. %1 - Text to search. %2 - Display mode. %3 - Dbrefs to check. %4 - Speaker.

@@ Visibility and Connections
&LASTIDLE [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideidle,%0))

&HIDECONN [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,hg),u(smalltime,conn(%0),3)))

&HIDEIDLE [u(cobj,mco)]=switch(objeval(%#,idle(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,u(ryg,round(mul(fdiv(bound(idle(%0),0,3600),3600),100),0))),u(smalltime,idle(%0),3)))

&LASTCONN [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideconn,%0))

@@ Color and Configuration

&MY_NAMES [u(cobj,mco)]=u(get_names,%@)
&GET_NAMES [u(cobj,mco)]=localize(u(filter,ISDBREF,edit(u(lattr,u(setr,accid,u(firstof,u(accid,%0),%0))/D`CONFIG`NAMES`#*),D`CONFIG`NAMES`,)))

&PLAYER_CONFIG [u(cobj,mco)]=localize(u(strfirstof,if(isdbref(u(setr,accid,u(accid,%0))),get(%q<accid>/D`CONFIG`%1`%2)),get(%0/D`CONFIG`%1`%2),get(u(cobj,pconf)/DEFAULT`%1`%2)))

&APPROVED [u(cobj,mco)]=u(isapproved,%0)
&APPROVED2 [u(cobj,mco)]=u(isapproved,%#)

&ISAPPROVED [u(cobj,mco)]=cor(u(isadmin,%0),if(u(game_config,SYSTEM,REQUIRE_APPROVAL),default(%0/D`APPROVED,0),1))

@@ EVENT HANDLING - PENN
&PLAYER`CONNECT [u(cobj,mco)]=@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`CONNECT)={@trigger ##/PLAYER`CONNECT=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/PLAYER`CONNECT=no_inherit

@@ Player-Approve is a pseudo-event triggered by the Account Management System.
&PLAYER`APPROVE [u(cobj,mco)]=@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`APPROVE)={@trigger ##/PLAYER`APPROVE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/PLAYER`APPROVE=no_inherit

&PLAYER`CREATE [u(cobj,mco)]=@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`CREATE)={@trigger ##/PLAYER`CREATE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/PLAYER`CREATE=no_inherit

&PLAYER`DISCONNECT [u(cobj,mco)]=@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,PLAYER`DISCONNECT)={@trigger ##/PLAYER`DISCONNECT=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/PLAYER`DISCONNECT=no_inherit

&SOCKET`LOGINFAIL [u(cobj,mco)]=@dolist/inline/nobreak u(filter,HASATTR,children(%!),%b,%b,SOCKET`LOGINFAIL)={@trigger ##/SOCKET`LOGINFAIL=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/SOCKET`LOGINFAIL=no_inherit

&SOCKET`CONNECT [u(cobj,mco)]=@check setr(banid,first(u(u(cao)/FUN`CHECKLOGINS,%1)));@select/inline gt(secs(),get(u(bandb)/%q<banid>`UNTIL))=1,{@cemit/noisy u(u(cao)/VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) EXPIRED Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) has connected.}},0,{@pemit/port %0=setr(message,strfirstof(get(u(bandb)/%q<banid>`MESSAGE),u(u(cao)/VAR`BANLIST`MESSAGE)));@boot/port %0;@cemit/noisy u(u(cao)/VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) attempted to connect. Message sent to them: '%q<message>'}}
@set [u(cobj,mco)]/SOCKET`CONNECT=no_inherit

&OBJECT`RENAME [u(cobj,mco)]=@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`RENAME)={@trigger ##/OBJECT`RENAME=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/OBJECT`RENAME=no_inherit

&OBJECT`DESTROY [u(cobj,mco)]=@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`DESTROY)={@trigger ##/OBJECT`DESTROY=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/OBJECT`DESTROY=no_inherit

&OBJECT`CREATE [u(cobj,mco)]=@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`CREATE)={@trigger ##/OBJECT`CREATE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/OBJECT`CREATE=no_inherit

&OBJECT`MOVE [u(cobj,mco)]=@dolist/inline u(filter,HASATTR,children(%!),%b,%b,OBJECT`MOVE)={@trigger ##/OBJECT`MOVE=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
@set [u(cobj,mco)]/OBJECT`MOVE=no_inherit

&SQL`CONNECT [u(cobj,mco)]=@sql SET time_zone = '+00:00';@nscemit/noisy u(cmo`staffrep)=SQL server connect: %0
&SQL`CONNECTFAIL [u(cobj,mco)]=@attach %!/INC`MSG`SYSCHAN={SQL server connect fail: %0}
&SQL`DISCONNECT [u(cobj,mco)]=@attach %!/INC`MSG`SYSCHAN={SQL server disconnect: %0}
&STARTUP`SQL [u(cobj,mco)]=@check sql(SELECT DATABASE());@trigger %!/LOOP`SQL;@trigger %!/LOOP`REINDEX
&LOOP`SQL [u(cobj,mco)]=@sql SELECT DATABASE();@wait 60=@trigger %!/LOOP`SQL
&LOOP`REINDEX [u(cobj,mco)]=@dolist sql(SHOW TABLES)=@sql ANALYZE TABLE %i0;@wait mul(60,60,24)=@trigger %!/LOOP`REINDEX

@@ EVENT HANDLING - RHOST

@ACONNECT [u(cobj,mco)]=@check strmatch(%va,RhostMUSH);@select/inline and(eq(totcmds(%#),0),lt(sub(secs(),ctime(%#)),10))=1,{@attach/nobreak %!/PLAYER`CREATE=%#,words(ports(%#)),first(ports(%#))};@attach/nobreak %!/PLAYER`CONNECT=%#,words(ports(%#)),first(ports(%#))
@set [u(cobj,mco)]/ACONNECT=no_inherit
@ADISCONNECT [u(cobj,mco)]=@check strmatch(%va,RhostMUSH);@attach/nobreak %!/PLAYER`DISCONNECT=%#,words(ports(%#)),ishidden(%#)
@set [u(cobj,mco)]/ADISCONNECT=no_inherit

&STARTUP`RHOSTEVENT [u(cobj,mco)]=@trigger %!/LOOP`RHOSTMUSH`EVENTHANDLER
&B_@NUKE [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@NUKE [u(cobj,mco)]=u(RHOST`DELETE`AFTER)
&B_@TOAD [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@TOAD [u(cobj,mco)]=u(RHOST`DELETE`AFTER)
&B_@TURTLE [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@TURTLE [u(cobj,mco)]=u(RHOST`DELETE`AFTER)
&B_@NAME [u(cobj,mco)]=u(RHOST`NAME`BEFORE)
&A_@NAME [u(cobj,mco)]=u(RHOST`NAME`AFTER)
&SUB_058 [u(cobj,mco)]=%#
&I_@EMIT [u(cobj,mco)]=not(isdbref(u(cobj,scene)))
&I_SAY [u(cobj,mco)]=not(isdbref(u(cobj,scene)))
&I_POSE [u(cobj,mco)]=not(isdbref(u(cobj,scene)))
&I_P [u(cobj,mco)]=not(isdbref(u(cobj,scene)))
&I_S [u(cobj,mco)]=not(isdbref(u(cobj,scene)))

&RHOST`DELETE`BEFORE [u(cobj,mco)]=if(u(setr,nuketarget,pmatch(before(rest(%m),=))),u(setq,nuketype,type(%q<nuketarget>))[u(setq,nukename,name(%q<nuketarget>))])
&RHOST`DELETE`AFTER [u(cobj,mco)]=if(cand(%q<nuketarget>,strmatch(%q<nuketype>,PLAYER)),if(cor(hasflag(%q<nuketarget>,RECOVERY),not(hastype(%q<target>,PLAYER))),u(attrib_set,u(cobj,mco),DELETE`QUEUE`[u(nextslot,u(cobj,mco),DELETE`QUEUE)],%q<nuketarget>|%q<nukename>|%q<nuketype>)))

&RHOST`NAME`BEFORE [u(cobj,mco)]=u(setq,namename,name(u(setr,nametarget,pmatch(before(rest(%m),=)))))
&RHOST`NAME`AFTER [u(cobj,mco)]=if(%q<nametarget>,if(strmatch(u(setr,newname,name(%q<nametarget>)),after(%m,=)),u(attrib_set,u(cobj,mco),NAME`QUEUE`[u(nextslot,u(cobj,mco),NAME`QUEUE)],%q<nametarget>|%q<newname>|%q<namename>)))

&LOOP`RHOSTMUSH`EVENTHANDLER [u(cobj,mco)]=@check strmatch(%va,RhostMUSH);@dolist/inline [u(lattr,%!/TRG`RHOSTMUSH`*)]={@trigger %!/%d0};@wait 60={@trigger me/LOOP`RHOSTMUSH`EVENTHANDLER}

&TRG`RHOSTMUSH`OBJECT`DELETE [u(cobj,mco)]=@dolist/inline u(lattr,u(cobj,mco)/DELETE`QUEUE`*)={@trigger u(cobj,mco)/OBJECT`DESTROY`RHOSTMUSH=elements(get(u(cobj,mco)/%d0),1,|),elements(get(u(cobj,mco)/%d0),2,|),elements(get(u(cobj,mco)/%d0),3,|);@attach %!/WIPE=u(cobj,mco),%d0}

&TRG`RHOSTMUSH`OBJECT`RENAME [u(cobj,mco)]=@dolist/inline u(lattr,u(cobj,mco)/NAME`QUEUE`*)={@trigger u(cobj,mco)/OBJECT`RENAME`RHOSTMUSH=elements(get(u(cobj,mco)/%d0),1,|),elements(get(u(cobj,mco)/%d0),2,|),elements(get(u(cobj,mco)/%d0),3,|);@attach %!/WIPE=u(cobj,mco),%d0}
