@@ DEPENDENCIES: Core

th u(NEWCOBJ,Report System,rep,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

@@ th u(newconf,CONFIG,REPORT,DEFAULT_CATEGORY,GENERAL,Name of default +request category?,WORD)
@@ th u(newconf,PCONF,REPORT,BRIEF_SCAN,0,ADMIN: +job/brief instead of +job/pending on login?,BOOL)
@@ th u(newconf,PCONF,REPORT,MYJOBS,1,Show +myjobs display at login?,BOOL)

&CMD`+REPORT`PENNMUSH [u(cobj,rep)]=$^(?s)(?\:\+)?(r|c)(POST|ADD|EDIT|REPLACE|LINK|COMPILE|REMOVE|EPORT|READ|NEW|NEXT|ASE|OVERVIEW|SCAN|COMMENT|ASES|EPORTS)(?\:/([A-z]+)?)?(?\:/(\d+)?)?(?\: +(?P<lsargs>(?P<targ>.+?)?(?\:/(?P<rtarg>.+?)?)?(?\:/(?P<ptarg>.+?)?)?)?)?(?\:=(?P<rsargs>.*))?$:@attach %!/CMD`+REPORT`MAIN
@set [u(cobj,rep)]/CMD`+REPORT`PENNMUSH=regexp
&CMD`+REPORT`RHOSTMUSH [u(cobj,rep)]=$^(?s)(?\:\+)?(report|case)(?\:/(\\[A-z\\]+)?)?(?\:/(\\d+)?)?(?\:/(#\\w+|old)?)?(?\: +(.*?))?(?\:/(\\d+)?)?(?\:=(.*))?$:@attach %!/CMD`+REPORT`MAIN
@set [u(cobj,rep)]/CMD`+REPORT`RHOSTMUSH=regexp
&CMD`+REPORT`MAIN [u(cobj,rep)]=th u(setq,sysname,switch(%1,r,REPORT,c,CASE));@check gte(strlen(%0),3);th u(setq,command,%2);@@ @attach %!/INC`PARTIAL=%2,setunion(u(COMMANDS`%1`PLAYER),if(u(isadmin,%#),u(COMMANDS`%1`ADMIN)),|,|),|,command,command;@attach %!/INC`GETSWITCH=%3;th u(setq,page,u(strfirstof,%4,1));@attach %!/INC`PLAYERID=%:;@@ @select/inline u(isadmin,%#)=1,{@pemit %#=0: %5\, 1: %9\, 2: %6\, 3: %7\, 4: %8};@attach %!/INC`%q<sysname>`%q<command>[if(%q<switch>,`%q<switch>)]=trim(%5),trim(%9),trim(%6),trim(%7),trim(%8);
@set [u(cobj,rep)]/CMD`+REPORT`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

@@ &INC`REPORT`EDIT [u(cobj,rep)]=@pemit %#=u(header,Testing Input);@pemit %#=iter(lnum(0,10),%i0: [r(%i0,args)],%b,%r);@pemit %#=u(footer)

@@ @pemit %#=u(header,Testing Input);@pemit %#=iter(lnum(0,10),%i0: [r(%i0,args)],%b,%r);@pemit %#=u(footer)

&SYSTEM`SWITCHES [u(cobj,rep)]=setunion(u(SWITCHES`%q<sysname>`%q<command>`PLAYER),if(u(isadmin,%#),u(SWITCHES`%q<sysname>`%q<command>`ADMIN)),|,|)

&SYSTEM`NAME [u(cobj,rep)]=u(strfirstof,%q<sysname>,REPORT)

&INC`PLAYERID [u(cobj,rep)]=@check objid(%0)=@attach %!/INC`MSG=ERROR: Invalid Objid!;@select/inline gt(u(setr,pid%1,u(mysql,get`playerid,%0)),0)=0,{@attach %!/INC`DOSQL=NEW`PLAYERID/pid%1,%0,name(%0)};@select/inline isdbref(u(setr,accobj%1,objid(u(accid,%0,1))))=1,{@select/inline gt(u(setr,aid%1,u(mysql,GET`ACCOUNTID,r(accobj%1))),0)=0,{@attach %!/INC`DOSQL=NEW`ACCOUNTID/aid%1,r(accobj%1),name(r(accobj%1))}},0,{th u(setq,aid%1,-1)};
&Q`GET`PLAYERID [u(cobj,rep)]=SELECT player_id FROM $PLAYERS$ WHERE objid=?
&Q`NEW`PLAYERID [u(cobj,rep)]=INSERT INTO $PLAYERS$ (objid,player_name) VALUES (?,?)
&Q`GET`ACCOUNTID [u(cobj,rep)]=SELECT id FROM $ACCOUNTS$ WHERE objid=?
&Q`NEW`ACCOUNTID [u(cobj,rep)]=INSERT INTO $ACCOUNTS$ (objid,name) VALUES (?,?)

&INC`GROUP [u(cobj,rep)]=@select/inline u(valnum,%2)=1,{@check strlen(u(setr,alldata,u(mysql3,FIND`CATEGORY,%2,%2)))=@attach %!/INC`MSG=ERROR: Group not found.;th iter(gid gname gobj,u(setq,%i0,elements(%q<alldata>,inum(0),chr(177))));th u(setq,gid1,before(%q<gobj>,:));th u(setq,gnum,%q<gid1>);},0,{@check isobjid(u(setr,gobj,objid(u(u(cobj,gms)/FUN`FINDGROUP,%2))))=@attach %!/INC`MSG=ERROR: Group not selected.;@attach %!/INC`GROUPID=%q<gobj>;th u(setq,gid1,before(%q<gobj>,:));th u(setq,gnum,%q<gid1>);th u(setq,gname,u(getproperty,%q<gnum>,GROUPNAME))}

&Q`FIND`CATEGORY [u(cobj,rep)]=SELECT id,name,objid FROM $CATEGORY$ where id=? OR name LIKE '!%%' LIMIT 1

&INC`GROUPID [u(cobj,rep)]=@check objid(%0)=@attach %!/INC`MSG=ERROR: Invalid Objid!;@select/inline gt(u(setr,gid%1,u(mysql,get`groupid,%0)),0)=0,{@attach %!/INC`DOSQL=NEW`GROUPID/gid%1,%0,name(%0)}
&Q`GET`GROUPID [u(cobj,rep)]=SELECT id FROM $CATEGORY$ WHERE objid=?
&Q`NEW`GROUPID [u(cobj,rep)]=INSERT INTO $CATEGORY$ (objid,name) VALUES (?,?)

&INC`CANREPORT [u(cobj,rep)]=@check isobjid(%q<gobj>)=@attach %!/INC`MSG=ERROR: Group is antiquated. Cannot edit.;@check u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPORT)=@attach %!/INC`MSG=ERROR: Permission denied. You require the REPORT privilege.

&INC`BROADCAST [u(cobj,rep)]=@message u(lwhoid)=%0,[u(cobj,gms)]/FUN`CHATFORMAT,##,%#,%0,OOC,2,%q<gid1>,,,%3

@@ &FUN`CHATFORMAT Group Management System <GMS>=if(cor(%6,%8),,switch(1,hasflag(%0,PARANOID),%[[name(%1)]%(%1%)%],hasflag(%0,NOSPOOF),%[[name(%1)]:%]))[ansi(c,<)][ansi(u(strfirstof,u(color,%0,GROUP,%5,,1),u(FUN`GETSET,%5,COLOR),hx),u(getmoniker,%5))][if(cand(%4,not(%8)),ansi(c,-)[switch(%4,2,REPORT,SYS)],switch(%3,OOC,ansi(c,-)[ansi(r,ucstr(%3))],IC,))][ansi(c,>)]%B[if(cor(%4,%8),u(colormarkup,%0,%2),if(u(FUN`GETSET,%5,CHANNELRANK),\[[u(strfirstof,u(FUN`GETRANK,%5,%1),?)]\]%B)[if(strlen(get(%1/D`GROUP`%5`TITLE)),get(%1/D`GROUP`%5`TITLE)%B)][u(speech,%1,u(colormarkup,%0,%2),%0,%7,GROUP,u(lwhoid))])]

&INC`REPORT`POST [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@check strlen(after(%0,/))=@attach %!/INC`MSG=ERROR: What will you title the report?;@stop u(mysql,CHECK`REPORT_TITLE,%q<gid>,lcstr(after(%0,/)),0)=@attach %!/INC`MSG=ERROR: Another report already uses this title.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered for the report text!;th u(setq,newid,add(u(mysql,HIGHEST`REPORT_ID,%q<gid>,0),1));@attach %!/INC`DOSQL=NEW`REPORT/repid,%q<gid>,after(%0,/),%q<pid>,%q<newid>,0,0;@attach %!/INC`DOSQL=NEW`COMMENT,%q<repid>,%q<pid>,0,1,%1;@attach %!/INC`DOSQL=UPDATE`HANDLER_MODE,%q<repid>,%q<pid>,2;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Report %q<newid>: '[after(%0,/)]' created for %q<gname>! Now be sure to add any other necessary /posts. /link scenes. then /compile.

&Q`HIGHEST`REPORT_ID [u(cobj,rep)]=SELECT max(display_id) FROM $REPORT$ WHERE category=? and mode=?
&Q`CHECK`REPORT_TITLE [u(cobj,rep)]=SELECT id FROM $REPORT$ where category=? AND LOWER(title)=? AND mode=?
&Q`NEW`REPORT [u(cobj,rep)]=INSERT INTO $REPORT$ (category,title,player,display_id,date_created,date_modified,mode,status) VALUES (?,?,?,?,UTC_TIMESTAMP(),UTC_TIMESTAMP(),?,?)

&Q`NEW`COMMENT [u(cobj,rep)]=INSERT INTO $REPORT_COMMENT$ (report,player,mode,display_id,date_created,date_modified,contents) VALUES(?,?,?,?,UTC_TIMESTAMP(),UTC_TIMESTAMP(),?)

&Q`UPDATE`HANDLER_MODE [u(cobj,rep)]=INSERT INTO $REPORT_HANDLER$ (report,player,mode,check_date) VALUES (?,?,?,UTC_TIMESTAMP()) ON DUPLICATE KEY UPDATE mode=VALUES(mode),check_date=VALUES(check_date)

&Q`UPDATE`HANDLER [u(cobj,rep)]=INSERT INTO $REPORT_HANDLER$ (report,player,check_date) VALUES (?,?,UTC_TIMESTAMP()) ON DUPLICATE KEY UPDATE check_date=VALUES(check_date)

&Q`UPDATE`ACCOUNT [u(cobj,rep)]=INSERT INTO $ACCOUNTREAD$ (report,account,check_date) VALUES (?,?,UTC_TIMESTAMP()) ON DUPLICATE KEY UPDATE check_date=VALUES(check_date)

&Q`EDIT`HANDLER_MODE [u(cobj,rep)]=INSERT INTO $REPORT_HANDLER$ (report,player,mode,check_date) VALUES (?,?,?,UTC_TIMESTAMP()) ON DUPLICATE KEY UPDATE mode=VALUES(mode)

&Q`UPDATE`REPORT [u(cobj,rep)]=UPDATE $REPORT$ set date_modified=UTC_TIMESTAMP() WHERE id=?

&INC`FINDREPORT [u(Cobj,rep)]=@check strlen(%3)=@attach %!/INC`MSG=ERROR: Report name/id field empty!;@check strlen(u(setr,res,u(mysql3,FIND`REPORT,%q<gid>,%3,sqlescape(%3),0)))=@attach %!/INC`MSG=ERROR: Report '%3' not found.;th null(iter(repid dispid title owner status,u(setq,%i0,elements(%q<res>,inum(0),chr(177)))));

&Q`FIND`REPORT [u(cobj,rep)]=SELECT id,display_id,title,player,status FROM $REPORT$ WHERE category=? and (display_id=? OR title LIKE '!%%') AND mode=? LIMIT 1

&INC`REPORT`COMMENT [u(cobj,rep)]=@attach %!/INC`REPORT`ADD
&INC`REPORT`ADD [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@check strlen(%1)=@attach %!/INC`MSG=ERROR: You have entered nothing for your post text!;th u(setq,newdisp,add(u(mysql,HIGHEST`COMMENT_ID,%q<repid>),1));@attach %!/INC`DOSQL=NEW`COMMENT,%q<repid>,%q<pid>,1,%q<newdisp>,%1;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Posted to %q<gname> - Report %q<dispid>: %q<title>;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n added [u(pueblize,Post %q<newdisp>,+report %q<gid>/%q<dispid>/%q<newdisp>)] to [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}

&Q`HIGHEST`COMMENT_ID [u(cobj,rep)]=SELECT max(display_id) FROM $REPORT_COMMENT$ where report=?

&INC`REPORT`EDIT [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: 'Before' section cannot be empty.;th u(setq,new,edit(elements(%q<res>,3,chr(177)),before(%1,/),after(%1,/)));@attach %!/INC`DOSQL=EDIT`COMMENT,%q<new>,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n edited [u(pueblize,Post %4,+report %q<gid>/%q<dispid>/%4)] on [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}

&Q`FIND`COMMENT_ID [u(cobj,rep)]=SELECT display_id,player,contents,id FROM $REPORT_COMMENT$ WHERE report=? and display_id=? AND status=0

&Q`EDIT`COMMENT [u(cobj,rep)]=UPDATE $REPORT_COMMENT$ set contents=? WHERE id=?

&INC`REPORT`REPLACE [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: 'Replace' section cannot be empty.;@attach %!/INC`DOSQL=EDIT`COMMENT,%1,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n replaced [u(pueblize,Post %4,+report %q<gid>/%q<dispid>/%4)] on [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}

&INC`REPORT`LINK [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@check u(setr,scene,u(u(cobj,scene)/mysql,get`scenecheck,%1))=@attach %!/INC`MSG=ERROR: Scene '%1' not found.;th u(setq,stitle,u(u(cobj,scene)/mysql,GET`SCENETITLE,%q<scene>));@select/inline u(mysql,EXIST`LINK,%q<repid>,%q<scene>)=>0,{@attach %!/INC`DOSQL=DELETE`LINK,%q<repid>,%q<scene>;@attach %!/INC`MSG=Scene %q<scene>: '%q<stitle>' unlinked!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n removed [u(pueblize,Scene %q<scene>: '%q<stitle>',+scene %q<scene>)] from [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}},{@attach %!/INC`DOSQL=ADD`LINK,%q<repid>,%q<scene>;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Scene %q<scene>: '%q<stitle>' linked!;@select/inline %q<status>=>0,{@attach %!/INC`MSG=[ansi(hr,WARNING:)] This Report is already compiled! Changes will not appear until you /compile it!;@attach %!/INC`BROADCAST={%n added [u(pueblize,Scene %q<scene>: '%q<stitle>',+scene %q<scene>)] to [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}}

&Q`EXIST`LINK [u(cobj,rep)]=SELECT event FROM $REPORT_EVENT$ WHERE report=? and event=? LIMIT 1
&Q`DELETE`LINK [u(cobj,rep)]=DELETE FROM $REPORT_EVENT$ WHERE report=? AND event=?
&Q`ADD`LINK [u(cobj,rep)]=INSERT INTO $REPORT_EVENT$ (report, event) VALUES (?,?)

&INC`REPORT`REMOVE [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@select/inline strlen(%4)=>0,{@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`MSG=Post %4 removed!;@attach %!/INC`DOSQL=DELETE`COMMENT,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n removed [u(pueblize,Post %4,+report %q<gid>/%q<dispid>/%4)] from [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}},{@attach %!/INC`REPORT`DELETE}

&Q`DELETE`COMMENT [u(cobj,rep)]=UPDATE $REPORT_COMMENT$ set status=10 where id=?

&INC`REPORT`DELETE [u(cobj,rep)]=@check u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will DELETE %q<gname> Report %q<repid>: '%q<title>'. This requires admin to undo! Are you sure? Enter command again to verify.},DELETE REPORT %q<repid>;@attach %!/INC`DOSQL=DELETE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@attach %!/INC`MSG=%q<gname> Report %q<dispid>: '%q<title>' deleted!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n DELETED [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2}

&Q`DELETE`REPORT [u(cobj,rep)]=UPDATE $REPORT$ set status=10 WHERE id=?

&INC`REPORT`COMPILE [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDREPORT;@check words(u(setr,scenes,u(mysql,GET`EVENTS,%q<repid>)))=@attach %!/INC`MSG=ERROR: No Scenes /link'd to compile!;@check words(u(setr,parts,setunion(iter(%q<scenes>,setdiff(u(u(cobj,scene)/mysql,GET`REPORT,%i0),u(u(cobj,scene)/mysql,GET`EXCLUDE,%i0))),)))=@attach %!/INC`MSG=ERROR: No participants to compile.;@dolist/inline MAJOR MINOR={th u(setq,##,u(sortgroups,iter(setunion(iter(%q<parts>,u(getproperty,%i0,##DBS)),),%i0)))};@check words(u(setr,locs,iter(u(sortname,setunion(iter(%q<scenes>,u(u(cobj,scene)/mysql,get`location,%i0)),)),u(getmoniker,%i0),%b,\,%b)))=@attach %!/INC`MSG=ERROR: No Scene locations!;th u(setq,unaff,u(sortname,u(filter,UNAFF,%q<parts>)));th u(setq,orig,%q<gid1>);th u(setq,majorbreak,iter(%q<major>,u(getproperty,u(setr,gid1,%i0),GROUPNAME)[ansi(hw,:)]%b[iter(u(u(cobj,gms)/sortrank,u(filter,MEMBER,%q<parts>,%b,%b,%i0)),u(getmoniker,%i0)%b\([get(%i0/D`GROUP`%i1`RANK)]\),%b,\,%b)],%b,%R));th u(setq,minorbreak,iter(%q<minor>,u(getproperty,u(setr,gid1,%i0),GROUPNAME)[ansi(hw,:)]%b[iter(u(u(cobj,gms)/sortrank,u(filter,MEMBER,%q<parts>,%b,%b,%i0)),u(getmoniker,%i0)%b\([get(%i0/D`GROUP`%i1`RANK)]\),%b,\,%b)],%b,%R));th u(setq,comp,[ansi(h,INVOLVED:)]%b[iter(u(sortname,%q<parts>),u(getmoniker,%i0),%b,\,%b)]%R[ansi(hw,LOCATIONS:)] %q<locs>[if(%q<majorbreak>,%R%R[ansi(hw,MAJOR AFFILIATION BREAKDOWN:)]%R%q<majorbreak>)][if(%q<minorbreak>,%R%R[ansi(hw,MINOR AFFILIATION BREAKDOWN:)]%R%q<minorbreak>)]);@attach %!/INC`DOSQL=COMPILE`REPORT,%q<comp>,%q<repid>;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Report compiled!;th u(setq,gid1,%q<orig>);@attach %!/INC`BROADCAST={%n COMPILED [u(pueblize,Report %q<dispid>: %q<title>,+report %q<gid>/%q<dispid>)]},OOC,2

&Q`GET`EVENTS [u(cobj,rep)]=SELECT event FROM $REPORT_EVENT$ where report=?

&FIL`UNAFF [u(cobj,rep)]=not(words(get(%0/D`GROUPS)))
&FIL`MEMBER [u(cobj,rep)]=t(match(get(%0/D`GROUPS),%1))

&Q`COMPILE`REPORT [u(cobj,rep)]=UPDATE $REPORT$ Set title_header=?,date_modified=UTC_TIMESTAMP(),status=1 WHERE id=?

&INC`REPORT`READ [u(cobj,rep)]=@attach %!/INC`REPORT`MAIN
&INC`REPORT`EPORTS [u(Cobj,rep)]=@attach %!/INC`REPORT`MAIN
&INC`REPORT`EPORT [u(Cobj,rep)]=@attach %!/INC`REPORT`MAIN
&INC`REPORT`MAIN [u(cobj,rep)]=@select/inline strlen(%0)=0,{@attach %!/INC`REPORT`LIST},{@select/inline strlen(%3)=0,{@attach %!/INC`REPORT`DISPLAY},{@attach %!/INC`REPORT`DETAILS}}

&FUN`ALLGROUPS [u(cobj,rep)]=localize(iter(u(sortgroups,u(u(cobj,gms)/filter,MAJORGROUPS,u(setr,groups,u(u(cobj,gms)/FUN`LISTGROUPS))))%b[u(sortgroups,u(u(cobj,gms)/filter,MINORGROUPS,%q<groups>))],objid(%i0)))

&INC`REPORT`NEW [u(cobj,rep)]=@attach %!/INC`REPORT`NEXT
&INC`REPORT`NEXT [u(cobj,rep)]=th null(iter(u(FUN`ALLGROUPS),if(strlen(u(setr,res,u(mysql3,LIST`UNREAD`FIRST,%q<pid>,%q<aid>,0,%i0))),ibreak())));@check strlen(%q<res>)=@attach %!/INC`MSG=Nothing new to display! Hooray!;@attach %!/INC`REPORT`DETAILS=,,elements(%q<res>,2,chr(177)),elements(%q<res>,1,chr(177))

&Q`LIST`UNREAD`FIRST [u(cobj,rep)]=SELECT r.display_id,r.category FROM $REPORT$ as r LEFT JOIN $REPORT_HANDLER$ as h ON r.id=h.report AND h.player=? LEFT JOIN $ACCOUNTREAD$ as acc ON acc.report=r.id AND acc.account=? LEFT JOIN $CATEGORY$ as c ON c.id=r.category WHERE r.mode=? AND c.objid=? AND ((h.check_date IS NULL AND acc.check_date IS NULL) OR (r.date_modified>h.check_date AND r.date_modified>acc.check_date)) order by r.display_id LIMIT 1

&FIL`NOREPORT [u(cobj,rep)]=not(u(u(cobj,gms)/FUN`GETSET,%0,NOREPORT))

&INC`REPORT`LIST [u(cobj,rep)]=th u(setq,groups,u(u(cobj,gms)/FUN`LISTGROUPS));th u(setq,groups,u(filter,NOREPORT,%q<groups>));th u(setq,major,u(sortgroups,u(u(cobj,gms)/filter,MAJORGROUPS,%q<groups>)));th u(setq,minor,u(sortgroups,u(u(cobj,gms)/filter,MINORGROUPS,%q<groups>)));@pemit %#=u(header,Reports - All Groups);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(30 3 16 3 16,Name,U,Reports,U,Cases));@dolist/inline Major Minor={@select/inline strlen(r(%i0))=>0,{@pemit %#=u(separator,%i0);@dolist/inline r(%i0)={@pemit %#=align(30 3 16 3 16,u(pueblize,u(getproperty,%i0,GROUPNAME),u(strfirstof,%0,+report) [name(%i0)]),if(u(setr,un,u(mysql,COUNT`UNREAD,%q<pid>,%q<aid>,0,objid(%i0))),ansi(hr,%q<un>),ansi(hx,0)),ljust(u(pueblize,u(mysql,TOTAL`REPORTS_OBJID,objid(%i0),0),+report [name(%i0)]),5),if(u(setr,un,u(mysql,COUNT`UNREAD,%q<pid>,1,objid(%i0))),ansi(hr,%q<un>),ansi(hx,0)),ljust(u(pueblize,u(mysql,TOTAL`REPORTS_OBJID,objid(%i0),1),+case [name(%i0)]),5))}}};th u(setq,gids,iter(%q<groups>,objid(%i0)));th u(setq,archived,u(filter,ARCHIVED,u(mysql,LIST`CATEGORY),%b,%b,%q<gids>));@select/inline words(%q<archived>)=>0,{@pemit %#=u(separator,Archived);@dolist/inline %q<archived>={th u(setq,data,u(mysql3,LOAD`CATEGORY,%i0));@pemit %#=align(30 3 16 3 16,u(pueblize,elements(%q<data>,3,chr(177))%b\([elements(%q<data>,1,chr(177))]\),+report [elements(%q<data>,1,chr(177))]),,u(mysql,TOTAL`REPORTS_OBJID,%i0,0),,u(mysql,TOTAL`REPORTS_OBJID,%i0,1))}};@pemit %#=u(FOOTER)

&FIL`ARCHIVED [u(cobj,rep)]=not(match(%1,%0))
&Q`LIST`CATEGORY [u(cobj,rep)]=SELECT objid FROM $CATEGORY$ ORDER BY name DESC
&Q`LOAD`CATEGORY [u(cobj,rep)]=SELECT id,objid,name FROM $CATEGORY$ WHERE objid=?

&Q`COUNT`UNREAD [u(cobj,rep)]=SELECT count(r.id) FROM $REPORT$ as r LEFT JOIN $REPORT_HANDLER$ as h ON r.id=h.report AND h.player=? LEFT JOIN $ACCOUNTREAD$ as acc ON acc.report=r.id AND acc.account=? LEFT JOIN $CATEGORY$ as c ON c.id=r.category WHERE r.mode=? AND c.objid=? AND ((h.check_date IS NULL AND acc.check_date IS NULL) OR (r.date_modified>h.check_date AND r.date_modified>acc.check_date))

&Q`TOTAL`REPORTS_OBJID [u(cobj,rep)]=SELECT count(r.id) FROM $REPORT$ as r LEFT JOIN $CATEGORY$ as c ON c.id=r.category WHERE c.objid=? AND mode=?

&INC`REPORT`DISPLAY [u(cobj,rep)]=@attach %!/INC`GROUP;th u(setq,total,u(mysql,TOTAL`REPORTS,%q<gid>,0));th u(setq,page,bound(%q<page>,1,u(setr,max,firstof(ceil(fdiv(%q<total>,30)),1))));th u(setq,offset,mul(30,sub(%q<page>,1)));@pemit %#=u(header,Reports - [u(getproperty,%q<gobj>,GROUPNAME)]);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(1 5 30 20 18,U,ID,Title,Creator,Created));@pemit %#=u(separator);@dolist/inline revwords(u(mysql,LIST`REPORTS,%q<gid>,0,%q<offset>))={th u(setq,data,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%i0));@pemit %#=align(1 5 30 20 18,if(gte(max(elements(%q<data>,4,chr(177)),elements(%q<data>,5,chr(177))),max(elements(%q<data>,7,chr(177)),elements(%q<data>,10,chr(177)))),ansi(hr,*),%b),u(pueblize,elements(%q<data>,1,chr(177)),+report %q<gid>/[elements(%q<data>,1,chr(177))]),elements(%q<data>,2,chr(177)),elements(%q<data>,3,chr(177)),u(fancytime,elements(%q<data>,4,chr(177)),%#))};@pemit %#=u(FOOTER,if(gt(%q<page>,1),ansi(hg,u(pueblize,<,+report/[sub(%q<page>,1)] %q<gid>)),ansi(hx,<))%BPage %q<page> of [bound(%q<max>,1)]%B[if(lt(%q<page>,%q<max>),ansi(hg,u(pueblize,>,+report/[add(%q<page>,1)] %q<gid>)),ansi(hx,>))])

&Q`TOTAL`REPORTS [u(cobj,rep)]=SELECT count(id) FROM $REPORT$ WHERE category=? AND status<10 and mode=?
&Q`LIST`REPORTS [u(cobj,rep)]=SELECT id FROM $REPORT$ WHERE category=? AND status<10 and mode=? ORDER BY display_id DESC LIMIT 30 OFFSET ?
&Q`LOAD`REPORT [u(cobj,rep)]=SELECT r.display_id,r.title,p.player_name,UNIX_TIMESTAMP(r.date_created),UNIX_TIMESTAMP(r.date_modified),h.mode,UNIX_TIMESTAMP(h.check_date),r.title_header,r.category,UNIX_TIMESTAMP(acc.check_date) FROM $REPORT$ as r LEFT JOIN $PLAYERS$ as p ON r.player=p.player_id LEFT JOIN $REPORT_HANDLER$ as h ON r.id=h.report AND h.player=? LEFT JOIN $ACCOUNTREAD$ as acc ON acc.report=r.id AND acc.account=? WHERE r.id=?

&INC`REPORT`DETAILS [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`FINDREPORT;th u(setq,data,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%q<repid>));@pemit %#=u(HEADER,u(getproperty,%q<gobj>,GROUPNAME) - Report %q<dispid>: '%q<title>');@pemit %#=u(strfirstof,elements(%q<data>,8,chr(177)),<uncompiled>);@select/inline words(u(setr,cases,u(mysql,GET`REPORT_CASES,%q<repid>)))=>0,{@pemit %#=u(separator,Cases);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(5 34 20 18,ID,Title,Creator,Date));@dolist/inline %q<cases>={th u(setq,ev,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%i0));@pemit %#=align(5 34 20 18,u(pueblize,elements(%q<ev>,1,chr(177)),+case [elements(%q<ev>,9,chr(177))]/[elements(%q<ev>,1,chr(177))]),u(pueblize,elements(%q<ev>,2,chr(177)),+case [elements(%q<ev>,9,chr(177))]/[elements(%q<ev>,1,chr(177))]),elements(%q<ev>,3,chr(177)),u(fancytime,elements(%q<ev>,4,chr(177)),%#))}};@select/inline words(u(setr,scenes,u(mysql,GET`EVENTS,%q<repid>)))=>0,{@pemit %#=u(separator,Events);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(5 34 20 18,ID,Title,Runner,Date));@dolist/inline %q<scenes>={th u(setq,ev,u(u(cobj,scene)/mysql3,GET`SCENEDETAILS,%i0));@pemit %#=align(5 34 20 18,u(pueblize,elements(%q<ev>,1,chr(177)),+log [elements(%q<ev>,1,chr(177))]),u(pueblize,elements(%q<ev>,2,chr(177)),+log [elements(%q<ev>,1,chr(177))]),elements(%q<ev>,4,chr(177)),u(fancytime,elements(%q<ev>,5,chr(177)),%#))}};@dolist/inline u(mysql,LIST`COMMENTS,%q<repid>)={th u(setq,com,u(mysql3,LOAD`COMMENT,%i0));@pemit %#=u(separator,elements(%q<com>,1,chr(177)): [elements(%q<com>,7,chr(177))] on [u(fancytime,elements(%q<com>,2,chr(177)),%#)]);@pemit %#=elements(%q<com>,5,chr(177))};@pemit %#=u(footer);@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>}

&Q`LIST`COMMENTS [u(cobj,rep)]=SELECT id FROM $REPORT_COMMENT$ WHERE status<10 AND report=? ORDER BY id ASC

&Q`GET`REPORT_CASES [u(cobj,rep)]=SELECT report FROM $CASE_FILE$ where parent=? ORDER BY report

&Q`LOAD`COMMENT [u(cobj,rep)]=SELECT c.display_id,UNIX_TIMESTAMP(c.date_created),UNIX_TIMESTAMP(c.date_modified),c.mode,c.contents,p.objid,p.player_name FROM $REPORT_COMMENT$ AS c LEFT JOIN $PLAYERS$ as p ON c.player=p.player_id WHERE c.id=?

&INC`REPORT`SCAN [u(cobj,rep)]=@pemit %#=u(header,Unread Reports);@dolist/inline/nobreak u(FUN`ALLGROUPS)={@check strlen(u(setr,res,u(mysql3,LIST`UNREAD`SCAN,%q<pid>,%q<aid>,0,%i0)));@pemit %#=u(getproperty,%i0,GROUPNAME): \([iter(%q<res>,u(pueblize,elements(%i0,1,chr(177)),+report [elements(%i0,2 1,chr(177),/)]),chr(176),\,%b)]\)};@pemit %#=u(footer)

&Q`LIST`UNREAD`SCAN [u(cobj,rep)]=SELECT r.display_id,r.category FROM $REPORT$ as r LEFT JOIN $REPORT_HANDLER$ as h ON r.id=h.report AND h.player=? LEFT JOIN $ACCOUNTREAD$ as acc ON acc.report=r.id AND acc.account=? LEFT JOIN $CATEGORY$ as c ON c.id=r.category WHERE r.mode=? AND c.objid=? AND ((h.check_date IS NULL AND acc.check_date IS NULL) OR (r.date_modified>h.check_date AND r.date_modified>acc.check_date)) order by r.display_id

@@ CASE section

&INC`CASE`POST [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@check strlen(after(%0,/))=@attach %!/INC`MSG=ERROR: What will you title the Case?;@stop u(mysql,CHECK`REPORT_TITLE,%q<gid>,lcstr(after(%0,/)),1)=@attach %!/INC`MSG=ERROR: Another report already uses this title.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered for the case overview!;th u(setq,newid,add(u(mysql,HIGHEST`REPORT_ID,%q<gid>,1),1));@attach %!/INC`DOSQL=NEW`REPORT/repid,%q<gid>,after(%0,/),%q<pid>,%q<newid>,1,1;@attach %!/INC`DOSQL=COMPILE`REPORT,%1,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER_MODE,%q<repid>,%q<pid>,2;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Case %q<newid>: '[after(%0,/)]' created for %q<gname>! Now be sure to add any other necessary /posts and /link reports.

&INC`FINDCASE [u(Cobj,rep)]=@check strlen(%3)=@attach %!/INC`MSG=ERROR: Case name/id field empty!;@check strlen(u(setr,res,u(mysql3,FIND`REPORT,%q<gid>,%3,sqlescape(%3),1)))=@attach %!/INC`MSG=ERROR: Case '%3' not found.;th null(iter(repid dispid title owner status,u(setq,%i0,elements(%q<res>,inum(0),chr(177)))));

&INC`CASE`COMMENT [u(cobj,rep)]=@attach %!/INC`CASE`ADD
&INC`CASE`ADD [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@check strlen(%1)=@attach %!/INC`MSG=ERROR: You have entered nothing for your post text!;th u(setq,newdisp,add(u(mysql,HIGHEST`COMMENT_ID,%q<repid>),1));@attach %!/INC`DOSQL=NEW`COMMENT,%q<repid>,%q<pid>,1,%q<newdisp>,%1;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Posted to %q<gname> - Case %q<dispid>: %q<title>;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n added [u(pueblize,Post %q<newdisp>,+case %q<gid>/%q<dispid>/%q<newdisp>)] to [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}

&INC`CASE`EDIT [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(before(%1,/))=@attach %!/INC`MSG=ERROR: 'Before' section cannot be empty.;th u(setq,new,edit(elements(%q<res>,3,chr(177)),before(%1,/),after(%1,/)));@attach %!/INC`DOSQL=EDIT`COMMENT,%q<new>,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n edited [u(pueblize,Post %4,+case %q<gid>/%q<dispid>/%4)] on [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}

&INC`CASE`REPLACE [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: 'Replace' section cannot be empty.;@attach %!/INC`DOSQL=EDIT`COMMENT,%1,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n replaced [u(pueblize,Post %4,+case %q<gid>/%q<dispid>/%4)] on [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}

&INC`CASE`LINK [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@check strlen(u(setr,repdata,u(mysql3,FIND`REPORT,%q<gid>,%1,%1,0)))=@attach %!/INC`MSG=ERROR: Report '%1' not found.;th u(setq,stitle,elements(%q<repdata>,3,chr(177)));th u(setq,scene,elements(%q<repdata>,1,chr(177)));th u(setq,repdisp,elements(%q<repdata>,2,chr(177)));@select/inline u(mysql,EXIST`LINK_REPORT,%q<repid>,%q<scene>)=>0,{@attach %!/INC`DOSQL=DELETE`LINK_REPORT,%q<repid>,%q<scene>;@attach %!/INC`MSG=Report %q<repdisp>: '%q<stitle>' unlinked!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n removed [u(pueblize,Report %q<repdisp>: '%q<stitle>',+report %q<gid>/%q<repdisp>)] from [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}},{@attach %!/INC`DOSQL=ADD`LINK_REPORT,%q<repid>,%q<scene>;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=Report %q<repdisp>: '%q<stitle>' linked!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n added [u(pueblize,Report %1: '%q<stitle>',+report %q<gid>/%1)] to [u(pueblize,Case %q<dispid>: %q<title>,+Case %q<gid>/%q<dispid>)]},OOC,2}}

&Q`EXIST`LINK_REPORT [u(cobj,rep)]=SELECT parent FROM $CASE_FILE$ WHERE report=? and parent=? LIMIT 1
&Q`DELETE`LINK_REPORT [u(cobj,rep)]=DELETE FROM $CASE_FILE$ WHERE report=? AND parent=?
&Q`ADD`LINK_REPORT [u(cobj,rep)]=INSERT INTO $CASE_FILE$ (report, parent) VALUES (?,?)

&INC`CASE`REMOVE [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@select/inline strlen(%4)=>0,{@check strlen(u(setr,res,u(mysql3,FIND`COMMENT_ID,%q<repid>,%4,0)))=@attach %!/INC`MSG=ERROR: Post %4 not found.;@check cor(eq(elements(%q<res>,2,chr(177)),%q<pid>),u(isadmin,%#),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`MSG=Post %4 removed!;@attach %!/INC`DOSQL=DELETE`COMMENT,elements(%q<res>,4,chr(177));@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n removed [u(pueblize,Post %4,+case %q<gid>/%q<dispid>/%4)] from [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}},{@attach %!/INC`CASE`DELETE}

&INC`CASE`DELETE [u(cobj,rep)]=@check u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN)=@attach %!/INC`MSG=ERROR: Permission denied.;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will DELETE %q<gname> Case %q<dispid>: '%q<title>'. This requires admin to undo! Are you sure? Enter command again to verify.},DELETE CASE %q<repid>;@attach %!/INC`DOSQL=DELETE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`REPORT,%q<repid>;@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};@attach %!/INC`MSG=%q<gname> Case %q<dispid>: '%q<title>' deleted!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n DELETED [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}

&INC`CASE`ASES [u(cobj,rep)]=@attach %!/INC`CASE`MAIN
&INC`CASE`ASE [u(cobj,rep)]=@attach %!/INC`CASE`MAIN
&INC`CASE`READ [u(cobj,rep)]=@attach %!/INC`CASE`MAIN
&INC`CASE`MAIN [u(cobj,rep)]=@select/inline strlen(%0)=0,{@attach %!/INC`CASE`LIST},{@select/inline strlen(%3)=0,{@attach %!/INC`CASE`DISPLAY},{@attach %!/INC`CASE`DETAILS}}

&INC`CASE`NEW [u(cobj,rep)]=@attach %!/INC`CASE`NEXT
&INC`CASE`NEXT [u(cobj,rep)]=th null(iter(u(FUN`ALLGROUPS),if(strlen(u(setr,res,u(mysql3,LIST`UNREAD`FIRST,%q<pid>,1,%i0))),ibreak())));@check strlen(%q<res>)=@attach %!/INC`MSG=Nothing new to display! Hooray!;@attach %!/INC`CASE`DETAILS=,,elements(%q<res>,2,chr(177)),elements(%q<res>,1,chr(177))

&INC`CASE`LIST [u(cobj,rep)]=@attach %!/INC`REPORT`LIST=+case

&INC`CASE`DISPLAY [u(cobj,rep)]=@attach %!/INC`GROUP;th u(setq,total,u(mysql,TOTAL`REPORTS,%q<gid>,0));th u(setq,page,bound(%q<page>,1,u(setr,max,firstof(ceil(fdiv(%q<total>,30)),1))));th u(setq,offset,mul(30,sub(%q<page>,1)));@pemit %#=u(header,Cases - [u(getproperty,%q<gobj>,GROUPNAME)]);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(1 5 30 20 18,U,ID,Title,Creator,Created));@pemit %#=u(separator);@dolist/inline revwords(u(mysql,LIST`REPORTS,%q<gid>,1,%q<offset>))={th u(setq,data,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%i0));@pemit %#=align(1 5 30 20 18,if(gte(max(elements(%q<data>,4,chr(177)),elements(%q<data>,5,chr(177))),max(elements(%q<data>,7,chr(177)),elements(%q<data>,10,chr(177)))),ansi(hr,*),%b),u(pueblize,elements(%q<data>,1,chr(177)),+case %q<gid>/[elements(%q<data>,1,chr(177))]),elements(%q<data>,2,chr(177)),elements(%q<data>,3,chr(177)),u(fancytime,elements(%q<data>,4,chr(177)),%#))};@pemit %#=u(FOOTER,if(gt(%q<page>,1),ansi(hg,u(pueblize,<,+case/[sub(%q<page>,1)] %q<gid>)),ansi(hx,<))%BPage %q<page> of [bound(%q<max>,1)]%B[if(lt(%q<page>,%q<max>),ansi(hg,u(pueblize,>,+case/[add(%q<page>,1)] %q<gid>)),ansi(hx,>))])

&INC`CASE`DETAILS [u(cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`FINDCASE;th u(setq,data,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%q<repid>));@pemit %#=u(HEADER,u(getproperty,%q<gobj>,GROUPNAME) - Case %q<dispid>: '%q<title>');@pemit %#=u(strfirstof,elements(%q<data>,8,chr(177)),<no overview>);@select/inline words(u(setr,reports,u(mysql,GET`CASE_REPORTS,%q<repid>)))=>0,{@pemit %#=u(separator,Reports);@pemit %#=ansi(u(color,%#,REPORT,COLUMN_NAMES),align(5 34 20 18,ID,Title,Creator,Date));@dolist/inline %q<reports>={th u(setq,ev,u(mysql3,LOAD`REPORT,%q<pid>,%q<aid>,%i0));@pemit %#=align(5 34 20 18,u(pueblize,elements(%q<ev>,1,chr(177)),+rread [elements(%q<ev>,9,chr(177))]/[elements(%q<ev>,1,chr(177))]),u(pueblize,elements(%q<ev>,2,chr(177)),+rread [elements(%q<ev>,9,chr(177))]/[elements(%q<ev>,1,chr(177))]),elements(%q<ev>,3,chr(177)),u(fancytime,elements(%q<ev>,4,chr(177)),%#))}};@dolist/inline u(mysql,LIST`COMMENTS,%q<repid>)={th u(setq,com,u(mysql3,LOAD`COMMENT,%i0));@pemit %#=u(separator,elements(%q<com>,1,chr(177)): [elements(%q<com>,7,chr(177))] on [u(fancytime,elements(%q<com>,2,chr(177)),%#)]);@pemit %#=elements(%q<com>,5,chr(177))};@pemit %#=u(footer);@attach %!/INC`DOSQL=UPDATE`HANDLER,%q<repid>,%q<pid>;@select/inline gt(%q<aid>,0)=1,{@attach %!/INC`DOSQL=UPDATE`ACCOUNT,%q<repid>,%q<aid>};

&Q`GET`CASE_REPORTS [u(cobj,rep)]=SELECT cf.parent FROM $CASE_FILE$ AS cf LEFT JOIN $REPORT$ AS r ON cf.parent=r.id WHERE cf.report=? AND r.status<10 ORDER BY cf.parent

&INC`CASE`OVERVIEW [u(Cobj,rep)]=@attach %!/INC`GROUP;@attach %!/INC`CANREPORT;@attach %!/INC`FINDCASE;@check cor(eq(%q<pid>,%q<owner>),u(u(cobj,gms)/FUN`GRPPERM,%:,%q<gobj>,REPADMIN))=@attach %!/INC`MSG=ERROR: Permission denied.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No text entered for the overview!;@attach %!/INC`DOSQL=COMPILE`REPORT,%1,%q<repid>;@attach %!/INC`MSG=Overview changed!;@select/inline %q<status>=>0,{@attach %!/INC`BROADCAST={%n changed the overview of [u(pueblize,Case %q<dispid>: %q<title>,+case %q<gid>/%q<dispid>)]},OOC,2}

&INC`CASE`SCAN [u(cobj,rep)]=@pemit %#=u(header,Unread Cases);@dolist/inline/nobreak u(FUN`ALLGROUPS)={@check strlen(u(setr,res,u(mysql3,LIST`UNREAD`SCAN,%q<pid>,%q<aid>,1,%i0)));@pemit %#=u(getproperty,%i0,GROUPNAME): \([iter(%q<res>,u(pueblize,elements(%i0,1,chr(177)),+case [elements(%i0,2 1,chr(177),/)]),chr(176),\,%b)]\)};@pemit %#=u(footer)

@@ INSTALL AND SQL CONFIG
&Q`INSTALL`CATEGORY [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $CATEGORY$ (id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,objid VARCHAR(30) NOT NULL,name VARCHAR(255) NOT NULL,PRIMARY KEY(id),UNIQUE (objid)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`REPORT [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $REPORT$ (id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,display_id BIGINT(31) UNSIGNED NOT NULL,title varchar(255) NOT NULL,category BIGINT(31) UNSIGNED NOT NULL,status TINYINT UNSIGNED NOT NULL DEFAULT 0,player BIGINT(31) UNSIGNED NOT NULL,title_header TEXT,date_created DATETIME,date_modified DATETIME,mode BOOL NOT NULL DEFAULT FALSE,PRIMARY KEY(id),FOREIGN KEY(category) REFERENCES $CATEGORY$(id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(player) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(category, title, mode, display_id)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`REPORT_HANDLER [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $REPORT_HANDLER$ (id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,report BIGINT(31) UNSIGNED NOT NULL,player BIGINT(31) UNSIGNED NOT NULL,mode TINYINT NOT NULL DEFAULT 0,check_date DATETIME,PRIMARY KEY(id),FOREIGN KEY(player) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(report) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(player, report)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`REPORT_EVENT [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $REPORT_EVENT$ (report BIGINT(31) UNSIGNED NOT NULL,event BIGINT(31) UNSIGNED NOT NULL,FOREIGN KEY(report) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(event) REFERENCES scene_scenes(scene_id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(report, event)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`REPORT_COMMENT [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $REPORT_COMMENT$ (id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,display_id BIGINT(31) UNSIGNED NOT NULL,report BIGINT(31) UNSIGNED NOT NULL,player BIGINT(31) UNSIGNED NOT NULL,mode TINYINT NOT NULL DEFAULT 0,status TINYINT UNSIGNED NOT NULL DEFAULT 0,date_created DATETIME,date_modified DATETIME,contents text,PRIMARY KEY(id),FOREIGN KEY(report) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(player) REFERENCES $PLAYERS$(player_id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(report, display_id)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`CASE_FILE [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $CASE_FILE$ (parent BIGINT(31) UNSIGNED NOT NULL,report BIGINT(31) UNSIGNED NOT NULL,FOREIGN KEY(report) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(parent) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(parent, report)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`ACCOUNTS [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $ACCOUNTS$ (id MEDIUMINT UNSIGNED NOT NULL AUTO_INCREMENT,objid VARCHAR(30) NOT NULL,name VARCHAR(255) NOT NULL,PRIMARY KEY(id),UNIQUE (objid)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&Q`INSTALL`ACCOUNTREAD [u(cobj,rep)]=CREATE TABLE IF NOT EXISTS $ACCOUNTREAD$ (id BIGINT(31) UNSIGNED NOT NULL AUTO_INCREMENT,report BIGINT(31) UNSIGNED NOT NULL,account MEDIUMINT UNSIGNED NOT NULL,check_date DATETIME,PRIMARY KEY(id),FOREIGN KEY(account) REFERENCES $ACCOUNTS$(id) ON UPDATE CASCADE ON DELETE CASCADE,FOREIGN KEY(report) REFERENCES $REPORT$(id) ON UPDATE CASCADE ON DELETE CASCADE,UNIQUE(account, report)) ENGINE=InnoDB DEFAULT CHARSET=latin1 AUTO_INCREMENT=1

&SQL`TABLES [u(cobj,rep)]=ACCOUNTS|CATEGORY|REPORT|REPORT_HANDLER|ACCOUNTREAD|REPORT_EVENT|REPORT_COMMENT|CASE_FILE
&SQL`TABLE`PLAYERS [u(cobj,rep)]=mushcode_players
&SQL`TABLE`ACCOUNTS [u(cobj,rep)]=mushcode_accounts
&SQL`TABLE`ACCOUNTREAD [u(cobj,rep)]=reports_accread
&SQL`TABLE`CATEGORY [u(cobj,rep)]=reports_category
&SQL`TABLE`REPORT [u(cobj,rep)]=reports_report
&SQL`TABLE`REPORT_COMMENT [u(cobj,rep)]=reports_comment
&SQL`TABLE`REPORT_EVENT [u(cobj,rep)]=reports_events
&SQL`TABLE`REPORT_HANDLER [u(cobj,rep)]=reports_handler
&SQL`TABLE`CASE_FILE [u(cobj,rep)]=cases_file

@trigger [u(cobj,rep)]/TRG`INSTALL

+help/add +report=[u(cobj,rep)]/HLP`+REPORT
+help/category +report=Communications
+help/metatags +report=+report report case +case
&HLP`+REPORT [u(cobj,rep)]=The Report System is used to manage in-character 'situation reports/after action reports' for roleplay. Reports and their overarching Case files are linked to the Group System. Each group maintains its own separate repository. For ease of use the system's commands are meant to mirror the BBS. While relevant group permissions are required for creating and managing reports, anyone can view them.%R%R[ansi(hc,Viewing Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+report)] - Show all Reports and Case categories.%R[ansi(h,+report <group>)] - View a specific group's Reports. Use +report/2\, +report/3\, etc to show different pages.%R[ansi(h,+report <group>/<report>)] - View specific report. <report> can be its ID or name \(partial match works.\)%R[ansi(h,+rread)] - This is an alias for +report. It works just the same. Including /page# trickery.)]%R%R[ansi(hc,Management Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+rpost <group>/<name>=<text>)] - Create a report! The entered text will be the first Comment for it.%R[ansi(h,+rcomment <group>/<report>=<text>)] - Create a new post for an existing report. Used for adding additional details.%R[ansi(h,+rreplace <group>/<report>/<comment#>=<text>)] - Made a mistake with a Comment? Use this to replace the text.%R[ansi(h,+redit <group>/<report>/<comment#>=<search>\,<replace>)] - Make minor changes to the text of a Comment. For advanced users.%R[ansi(h,+rremove <group>/<report>)] - Deletes a report. Requires REPADMIN permission for Group.%R[ansi(h,+rremove <group>/<report>/<comment#>)] - If your comment no longer works at all\, this deletes it.%R[ansi(h,+rlink <group>/<report>=<scene ID>)] - Links a Scene from SceneSys to the Report. Use again on the same ID to remove. Reports can have more than one Scene linked!%R[ansi(h,+rcompile <group>/<report>)] - 'Finalize' a report. This will generate Location/Participant data for the Report's header. After reports are compiled new activity will be announced on their relevant Group channels.)]%R%RThe Case System is used for grouping together many Reports under a single topic.%R%R[ansi(hc,Case Commands)]%R[align(5 [sub(u(width,%#),6)],,All commands work exactly as [ansi(h,+report)] does. Example: +cpost\, +cedit\, and so on. Differences:%R%R[ansi(h,+clink <group>/<case>=<report>)] - Link a report to the Case. Cases can link more than one report. Use again to remove. <report> can be its ID or title.%R[ansi(h,+coverview <group>/<case>=<text>)] - Replace the Overview for a case that's already been created.%RCase does not have a +ccompile.)]
